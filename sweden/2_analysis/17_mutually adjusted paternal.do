
/*******************************************************************************

	Generate table showing the adjusted and mutually adjusted paternal negative control analyses for the supplement

	Author: Flo Martin

	Date: 11/12/2023

	Table generated by this script
	
		- mutually adjusted pat.txt mutually adjusted paternal negative control logisitic regression models for each outcome
		
*******************************************************************************/

* Start logging

	log using "$Logdir\2_analysis\17_mutually adjusted paternal", name(mutually_adjusted_paternal) replace
	
********************************************************************************	

	tempname myhandle	
	file open `myhandle' using "$Tabledir\mutually adjusted pat.txt", write replace
	
	file write `myhandle' "Outcome" _tab "Total complete dads" _tab "Paternal exposed n/N (%)" _tab "Paternal unexposed n/N (%)" _tab "aOR" _tab "maOR" _n
	
	use "$Deriveddir\paternal_analysis_dataset_reduced.dta", clear
	
		* Ensure complete case analysis
	gen cc = 1 if birth_yr_cat!=. & mother_age_cat!=. & mother_educ!=. & parity!=. & ap_12mo!=. & asm_12mo!=. & mat_depression!=. & mat_anxiety!=. & pat_depression!=. & pat_anxiety!=. & father_educ!=.
	keep if cc==1
	
	foreach outcome in stillborn neonatal_death preterm postterm sga_pct lga_pct apgar5_bin { 
		
		* Recode incident users after 37 weeks' unexposed for the preterm delivery modesls
			local y="`outcome'"
			if "`y'"=="preterm" {
				
				gen any_preg_pat_preterm = 1 if any_preg_pat==1
				replace any_preg_pat_preterm = 0 if any_preg_pat==0
				replace any_preg_pat_preterm=0 if flag_37wk_initiators==1
				tab any_preg_pat_preterm
				
				count if `outcome'!=.
				local total=`r(N)'
				
				count if  `outcome'==1  & any_preg_pat_preterm==1
				local n_exp=`r(N)'
							
				count if  `outcome'==1  & any_preg_pat_preterm==0
				local n_unexp=`r(N)'
									
				count if `outcome'!=. & any_preg_pat_preterm==1
				local total_exp=`r(N)'
				local percent_exp=(`n_exp'/`total_exp')*100
							
				count if `outcome'!=. & any_preg_pat_preterm==0
				local total_unexp=`r(N)'
				local percent_unexp=(`n_unexp'/`total_unexp')*100
				
			}
			
			else {
				
				tab any_preg_pat
				
				count if `outcome'!=.
				local total=`r(N)'
				
				count if  `outcome'==1  & any_preg_pat==1
				local n_exp=`r(N)'
							
				count if  `outcome'==1  & any_preg_pat==0
				local n_unexp=`r(N)'
									
				count if `outcome'!=. & any_preg_pat==1
				local total_exp=`r(N)'
				local percent_exp=(`n_exp'/`total_exp')*100
							
				count if `outcome'!=. & any_preg_pat==0
				local total_unexp=`r(N)'
				local percent_unexp=(`n_unexp'/`total_unexp')*100
				
			}
						
		file write `myhandle' _tab %9.0fc (`total') _tab %7.0fc (`n_exp') ("/") %7.0fc (`total_exp') (" (") %4.2f (`percent_exp') (")") _tab %7.0fc (`n_unexp') ("/") %9.0fc (`total_unexp') (" (") %4.2f (`percent_unexp') (")")
		
		* Adjusted for all covariates	
			
		if "`y'"=="preterm" {
			
			logistic `outcome' i.any_preg_pat_preterm birth_yr i.mother_age_cat i.mother_educ i.dispink5atbirth i.mother_birth_country_nonsverige i.mother_bmi_cat i.smoke_preg i.prev_sb_bin i.parity i.ap_12mo i.asm_12mo mat_depression mat_anxiety pat_depression pat_anxiety, vce(cluster mother_id) or
			
			local tot=`e(N)'
			lincom 1.any_preg_pat_preterm, or 
			local minadjor=`r(estimate)'
			local minadjuci=`r(ub)'
			local minadjlci=`r(lb)'
			
		}
		
		else {
			
			logistic `outcome' i.any_preg_pat birth_yr i.mother_age_cat i.mother_educ i.dispink5atbirth i.mother_birth_country_nonsverige i.mother_bmi_cat i.smoke_preg i.prev_sb_bin i.parity i.ap_12mo i.asm_12mo mat_depression mat_anxiety pat_depression pat_anxiety, vce(cluster mother_id) or
			
			local tot=`e(N)'
			lincom 1.any_preg_pat, or 
			local minadjor=`r(estimate)'
			local minadjuci=`r(ub)'
			local minadjlci=`r(lb)'
			
		}
						
		file write `myhandle' _tab %4.2f (`minadjor') (" (") %4.2f (`minadjlci') ("-") %4.2f (`minadjuci') (")") 
		
		* Mutually adjusted for all covariates plus maternal antidepressant use during pregnancy
		
		if "`y'"=="preterm" {
		
			logistic `outcome' i.any_preg_pat_preterm birth_yr i.mother_age_cat i.mother_educ i.dispink5atbirth i.mother_birth_country_nonsverige i.mother_bmi_cat i.smoke_preg i.prev_sb_bin i.parity i.ap_12mo i.asm_12mo mat_depression mat_anxiety pat_depression pat_anxiety any_preg_mat, or vce(cluster mother_id)
		
			local tot=`e(N)'
			lincom 1.any_preg_pat_preterm, or  
			local minadjor=`r(estimate)'
			local minadjuci=`r(ub)'
			local minadjlci=`r(lb)'
			
		}
		
		else {
			
			logistic `outcome' i.any_preg_pat birth_yr i.mother_age_cat i.mother_educ i.dispink5atbirth i.mother_birth_country_nonsverige i.mother_bmi_cat i.smoke_preg i.prev_sb_bin i.parity i.ap_12mo i.asm_12mo mat_depression mat_anxiety pat_depression pat_anxiety any_preg_mat, or vce(cluster mother_id)
		
			local tot=`e(N)'
			lincom 1.any_preg_pat, or  
			local minadjor=`r(estimate)'
			local minadjuci=`r(ub)'
			local minadjlci=`r(lb)'
			
		}

		file write `myhandle' _tab %4.2f (`minadjor') (" (") %4.2f (`minadjlci') ("-") %4.2f (`minadjuci') (")") _n
						
	}
	
********************************************************************************

* Stop logging, translate .smcl into .pdf and erase .smcl

	log close mutually_adjusted_paternal
	
	translate "$Logdir\2_analysis\17_mutually adjusted paternal.smcl" "$Logdir\2_analysis\17_mutually adjusted paternal.pdf", replace
	
	erase "$Logdir\2_analysis\17_mutually adjusted paternal.smcl"
	
********************************************************************************
