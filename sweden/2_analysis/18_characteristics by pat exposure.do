
/*********************************************************************

	Generate table 1 for Swedish registry project for antidepressant use during pregnancy and birth outcomes (mimicing the CPRD characteristics table as closely as possible)

	Author: Flo Martin

	Date: 	11/09/2023

	Table generated by this script
	
		- table 1.txt characteristics table
		
*********************************************************************/

* Start logging

	log using "$Logdir\2_analysis\18_characteristics by pat", name(characteristics_by_pat) replace
	
********************************************************************************	

	* Generic code to output one row of table
	cap prog drop generaterow

	program define generaterow
		
		syntax, variable(varname) condition(string) outcome(string)
	
		* Put the varname and condition to left so that alignment can be checked vs shell
		file write tablecontent "" _tab
		
		cou
		local overalldenom=r(N)
		
		cou if `variable' `condition'
		local rowdenom = r(N)
		local colpct = 100*(r(N)/`overalldenom')
		file write tablecontent %11.0fc (`rowdenom') (" (") %4.1f (`colpct') (")") _tab

		cou if any_preg_pat==1 
		local coldenom = r(N)
		cou if any_preg_pat==1 & `variable' `condition'
		local pct = 100*(r(N)/`coldenom')
		file write tablecontent %11.0fc (r(N)) (" (") %4.1f (`pct') (")") _tab

		cou if any_preg_pat==0 
		local coldenom = r(N)
		cou if any_preg_pat==0 & `variable' `condition'
		local pct = 100*(r(N)/`coldenom')
		file write tablecontent %11.0fc (r(N)) (" (") %4.1f (`pct') (")") _n
	
	end


**********************************************************************

* Generic code to output one section (varible) within table (calls above)

	cap prog drop tabulatevariable
	prog define tabulatevariable
		
		syntax, variable(varname) start(real) end(real) [missing] outcome(string)

		foreach varlevel of numlist `start'/`end'{ 
		
			generaterow, variable(`variable') condition("==`varlevel'") outcome(any_preg_pat)
	
		}
	
		if "`missing'"!="" generaterow, variable(`variable') condition(">=.") outcome(any_preg_pat)

	end

**********************************************************************

* Set up output file

		use "$Deriveddir\paternal_analysis_dataset.dta", clear
		
		recode mother_birth_country_nonsverige .=9
		recode mother_bmi_cat .=9
		recode smoke_preg .=9
		recode parity .=9
		recode prev_sb_bin .=9
		recode spont_labour .=9 
		recode dispink5atbirth .=9

**********************************************************************/
* 2 - Prepare formats for data for output
**********************************************************************

		cap file close tablecontent
		file open tablecontent using "$Tabledir\characteristic table pat.txt", write text replace

		file write tablecontent "Variable" _tab "Level" _tab "Total" _tab "Exposed to antidepressants during pregnancy" _tab "Unexposed to antidepressants during pregnancy" _n
		
		file write tablecontent "Total" 
		gen byte total=1
		tabulatevariable, variable(total) start(1) end(1) outcome(any_preg_pat)
		
*********************************************************************************

* Year of pregnancy
		
		file write tablecontent "Year of birth" _n

		file write tablecontent "1996-1999" 
		tabulatevariable, variable(birth_yr_cat) start(1) end(1) outcome(any_preg_pat)
		
		file write tablecontent "2000-2004" 
		tabulatevariable, variable(birth_yr_cat) start(2) end(2) outcome(any_preg_pat) 
		
		file write tablecontent "2005-2009" 
		tabulatevariable, variable(birth_yr_cat) start(3) end(3) outcome(any_preg_pat)
		
		file write tablecontent "2010-2015" 
		tabulatevariable, variable(birth_yr_cat) start(4) end(4) outcome(any_preg_pat) 
		
		file write tablecontent "2016-2020" 
		tabulatevariable, variable(birth_yr_cat) start(5) end(5) outcome(any_preg_pat) 
	
*********************************************************************************

* Maternal age
	
		file write tablecontent "Maternal age at start of pregnancy" _n
		
		file write tablecontent "<20" 
		tabulatevariable, variable(mother_age_cat) start(0) end(0) outcome(any_preg_pat) 
		
		file write tablecontent "20-24" 
		tabulatevariable, variable(mother_age_cat) start(1) end(1) outcome(any_preg_pat) 
		
		file write tablecontent "25-29" 
		tabulatevariable, variable(mother_age_cat) start(2) end(2) outcome(any_preg_pat)
		
		file write tablecontent "30-34" 
		tabulatevariable, variable(mother_age_cat) start(3) end(3) outcome(any_preg_pat) 
		
		file write tablecontent "35-39" 
		tabulatevariable, variable(mother_age_cat) start(4) end(4) outcome(any_preg_pat)
		
		file write tablecontent "40-44" 
		tabulatevariable, variable(mother_age_cat) start(5) end(5) outcome(any_preg_pat)
		
		file write tablecontent "45+" 
		tabulatevariable, variable(mother_age_cat) start(6) end(6) outcome(any_preg_pat)
		
*********************************************************************************

* Maternal educational attainment - not available in UK

	file write tablecontent "Maternal educational attainment at the start of pregnancy" _n
	file write tablecontent "Compulsory or less"
	tabulatevariable, variable(mother_educ) start(0) end(0) outcome(any_preg_pat)
	
	file write tablecontent "Secondary"
	tabulatevariable, variable(mother_educ) start(1) end(1) outcome(any_preg_pat)
	
	file write tablecontent "Post-secondary"
	tabulatevariable, variable(mother_educ) start(2) end(2) outcome(any_preg_pat)
	
	file write tablecontent "Post-graduate"
	tabulatevariable, variable(mother_educ) start(3) end(3) outcome(any_preg_pat)
	
	file write tablecontent "Missing (likely educated overseas)"
	tabulatevariable, variable(mother_educ) start(4) end(4) outcome(any_preg_pat)

*********************************************************************************

* Household disposable income - not available in UK or Norway

	file write tablecontent "Household disposable income at the start of pregnancy (in quintiles)" _n
	
	file write tablecontent "1"
	tabulatevariable, variable(dispink5atbirth) start(1) end(1) outcome(any_preg_pat)
	
	file write tablecontent "2"
	tabulatevariable, variable(dispink5atbirth) start(2) end(2) outcome(any_preg_pat)
	
	file write tablecontent "3"
	tabulatevariable, variable(dispink5atbirth) start(3) end(3) outcome(any_preg_pat)
	
	file write tablecontent "4"
	tabulatevariable, variable(dispink5atbirth) start(4) end(4) outcome(any_preg_pat)
	
	file write tablecontent "5"
	tabulatevariable, variable(dispink5atbirth) start(5) end(5) outcome(any_preg_pat)
	
	file write tablecontent "Missing"
	tabulatevariable, variable(dispink5atbirth) start(9) end(9) outcome(any_preg_pat)

*********************************************************************************

* Practice index of multiple deprivation
		
		file write tablecontent "Practice IMD (in quintiles)" _n

		file write tablecontent "1" _n
		
		file write tablecontent "2" _n
		
		file write tablecontent "3" _n
		
		file write tablecontent "4" _n
		
		file write tablecontent "5" _n
	
*********************************************************************************

* Maternal country of birth - not available in UK

	file write tablecontent "Maternal country of birth" _n
	
	file write tablecontent "Native-born"
	tabulatevariable, variable(mother_birth_country_nonsverige) start(1) end(1) outcome(any_preg_pat)
	
	file write tablecontent "Missing"
	tabulatevariable, variable(mother_birth_country_nonsverige) start(9) end(9) outcome(any_preg_pat)

*********************************************************************************

* Maternal ethnicity		
		
		file write tablecontent "Maternal ethnicity" _n

		file write tablecontent "White" _n
		
		file write tablecontent "South Asian" _n
		
		file write tablecontent "Black" _n
		
		file write tablecontent "Other" _n
		
		file write tablecontent "Mixed" _n
		
		file write tablecontent "Missing" _n
		
*********************************************************************************

* Maternal body mass index
		
		file write tablecontent "Maternal body mass index (BMI)" _n

		file write tablecontent "Underweight (<18.5 kg/m^2)" 
		tabulatevariable, variable(mother_bmi_cat) start(0) end(0) outcome(any_preg_pat)
		
		file write tablecontent "Healthy weight (18.5-24.9 kg/m^2)" 
		tabulatevariable, variable(mother_bmi_cat) start(1) end(1) outcome(any_preg_pat)
		
		file write tablecontent "Overweight (25.0-29.9 kg/m^2)" 
		tabulatevariable, variable(mother_bmi_cat) start(2) end(2) outcome(any_preg_pat)
		
		file write tablecontent "Obese (>=30.0 kg/m^2)" 
		tabulatevariable, variable(mother_bmi_cat) start(3) end(3) outcome(any_preg_pat)
		
		file write tablecontent "Missing" 
		tabulatevariable, variable(mother_bmi_cat) start(9) end(9) outcome(any_preg_pat)
		
*********************************************************************************

* Smoking status during pregnancy

		file write tablecontent "Maternal smoking during pregnancy" _n
		
		file write tablecontent "Smoker"
		tabulatevariable, variable(smoke_preg) start(1) end(1) outcome(any_preg_pat)
		
		file write tablecontent "Missing"
		tabulatevariable, variable(smoke_preg) start(9) end(9) outcome(any_preg_pat)
		
*********************************************************************************
		
* History of stillbirth
		
		file write tablecontent "Maternal history of stillbirth at the start of pregnancy" _n

		file write tablecontent "History of stillbirth" 
		tabulatevariable, variable(prev_sb_bin) start(1) end(1) outcome(any_preg_pat)
		
		file write tablecontent "Missing" 
		tabulatevariable, variable(prev_sb_bin) start(9) end(9) outcome(any_preg_pat)
		
*********************************************************************************

* Parity

		file write tablecontent "Maternal parity at the start of pregnancy" _n
		
		file write tablecontent "0"
		tabulatevariable, variable(parity) start(0) end(0) outcome(any_preg_pat)
		
		file write tablecontent "1"
		tabulatevariable, variable(parity) start(1) end(1) outcome(any_preg_pat)
		
		file write tablecontent "2"
		tabulatevariable, variable(parity) start(2) end(2) outcome(any_preg_pat)
		
		file write tablecontent "3"
		tabulatevariable, variable(parity) start(3) end(3) outcome(any_preg_pat)
		
		file write tablecontent "4+"
		tabulatevariable, variable(parity) start(4) end(4) outcome(any_preg_pat)
		
		file write tablecontent "Missing"
	tabulatevariable, variable(parity) start(9) end(9) outcome(any_preg_pat)
		
*********************************************************************************

* Maternal indications for antidepressants
		
		file write tablecontent "Maternal antidepressant indications ever before pregnancy" _n

		file write tablecontent "Depression" 
		tabulatevariable, variable(mat_depression) start(1) end(1) outcome(any_preg_pat)
		
		file write tablecontent "Anxiety" 
		tabulatevariable, variable(mat_anxiety) start(1) end(1) outcome(any_preg_pat)
		
		file write tablecontent "Eating disorders" 
		tabulatevariable, variable(mat_ed) start(1) end(1) outcome(any_preg_pat)
		
*********************************************************************************

* Other indications for antidepressants
		
		file write tablecontent "Other possible indications for antidepressants" _n

		file write tablecontent "Other affective disorders" _n
		
		file write tablecontent "Diabetic neuropathy" _n
		
		file write tablecontent "Migraine prophylaxis" 
		tabulatevariable, variable(mat_migraine) start(1) end(1) outcome(any_preg_pat)
		
		file write tablecontent "Narcolepsy with cataplexy" _n
		
		file write tablecontent "Pain" _n
		
		file write tablecontent "Stress incontinence" 
		tabulatevariable, variable(mat_incont) start(1) end(1) outcome(any_preg_pat)
		
		file write tablecontent "Tension-type headache" 
		tabulatevariable, variable(mat_headache) start(1) end(1) outcome(any_preg_pat)
		
*********************************************************************************

/* Other maternal comorbidities
		
		file write tablecontent "Chronic illness ever before pregnancy" _n

		file write tablecontent "Asthma" _n 
		
		file write tablecontent "Chronic hypertension" 
		tabulatevariable, variable(hypbp) start(1) end(1) outcome(any_preg_pat)
		
		file write tablecontent "Chronic renal disease" _n 
		
		file write tablecontent "Epilepsy" _n 
		
		file write tablecontent "Chronic diabetes" 
		tabulatevariable, variable(diab) start(1) end(1) outcome(any_preg_pat)
		
*********************************************************************************/

* Other prescriptions in the 12 months prior to pregnancy

		file write tablecontent "Other mental health-related prescriptions in 12 months before pregnancy" _n
		
		file write tablecontent "Antipsychotics" 
		tabulatevariable, variable(ap_12mo) start(1) end(1) outcome(any_preg_pat)
		
		file write tablecontent "Anti-seizure medications" 
		tabulatevariable, variable(asm_12mo) start(1) end(1) outcome(any_preg_pat) 
		
*********************************************************************************

* Number of CPRD consultations in the 12 months prior to pregnancy
		
		file write tablecontent "Primary care consultations in the 12 months before pregnancy" _n
		
		file write tablecontent "0" _n
		
		file write tablecontent "1-3" _n
		
		file write tablecontent "4-10" _n
		
		file write tablecontent ">10" _n
	
*********************************************************************************

* Supplement use pre-pregnancy

		file write tablecontent "Supplement use reported before pregnancy" _n
		
		file write tablecontent "Folic acid" _n
		
*********************************************************************************

* Iniiation of labour
		
		file write tablecontent "Initiation of labour" _n
		
		file write tablecontent "Induced" 
		tabulatevariable, variable(induction) start(1) end(1) outcome(any_preg_pat)
		
		file write tablecontent "Missing" 
		tabulatevariable, variable(induction) start(9) end(9) outcome(any_preg_pat)
		
***************************************************** ***************************
		
		file write tablecontent "Paternal characteristics" _n
		
********************************************************************************
		
		file write tablecontent "Paternal educational attainment" _n

		file write tablecontent "Compulsory or less"
		tabulatevariable, variable(father_educ) start(0) end(0) outcome(any_preg_pat)
			
		file write tablecontent "Secondary"
		tabulatevariable, variable(father_educ) start(1) end(1) outcome(any_preg_pat)
			
		file write tablecontent "Post-secondary"
		tabulatevariable, variable(father_educ) start(2) end(2) outcome(any_preg_pat)
			
		file write tablecontent "Post-graduate"
		tabulatevariable, variable(father_educ) start(3) end(3) outcome(any_preg_pat)
			
		file write tablecontent "Missing (likely educated overseas)"
		tabulatevariable, variable(father_educ) start(4) end(4) outcome(any_preg_pat)
		
********************************************************************************
		
		file write tablecontent "Paternal indication before the start of pregnancy" _n

		file write tablecontent "Depression" 
		tabulatevariable, variable(pat_depression) start(1) end(1) outcome(any_preg_pat)
		
		file write tablecontent "Anxiety" 
		tabulatevariable, variable(pat_anxiety) start(1) end(1) outcome(any_preg_pat)

********************************************************************************		

		file close tablecontent
		
********************************************************************************

* Stop logging, translate .smcl into .pdf and erase .smcl

	log close characteristics_by_pat
	
	translate "$Logdir\2_analysis\18_characteristics by pat.smcl" "$Logdir\2_analysis\18_characteristics by pat.pdf", replace
	
	erase "$Logdir\2_analysis\18_characteristics by pat.smcl"
	
********************************************************************************
