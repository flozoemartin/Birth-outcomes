
/*******************************************************************************

	Generate table showing the primary and negative control analysis models of antidepressant use during pregnancy and the outcomes of interest

	Author: Flo Martin

	Date: 26/09/2023

	Table generated by this script
	
		- primary analysis.txt primary analysis logisitic regression models for each outcome
		
*******************************************************************************/

* Start logging

	log using "$Logdir\2_analysis\20_primary analysis in sibs", name(primary_analysis_in_sibs) replace
	
********************************************************************************	

	tempname myhandle	
	file open `myhandle' using "$Tabledir\primary analysis_mat.txt", write replace
	
	file write `myhandle' "Outcome" _tab "Total complete mums" _tab "Maternal exposed n/N (%)" _tab "Maternal unexposed n/N (%)" _tab "OR" _tab "aOR*" _n
	
	use "$Deriveddir\sibling_analysis.dta", clear
	
	* Ensure complete case analysis
	gen cc = 1 if birth_yr!=. & mother_age_cat!=. & mother_educ!=. & dispink5atbirth!=. & mother_birth_country_nonsverige!=. & mother_bmi_cat!=. & smoke_beg!=. & prev_sb_bin!=. & parity!=. & ap_12mo!=. & asm_12mo!=. & depression!=. & anxiety!=.
	keep if cc==1
	
	foreach outcome in stillborn neonatal_death preterm postterm sga_pct lga_pct apgar5_bin { 
		
		* Recode incident users after 37 weeks' unexposed for the preterm delivery modesls
		local y="`outcome'"
		if "`y'"=="preterm" {
		    
			gen any_preg_preterm=1 if any_preg==1
			replace any_preg_preterm=0 if any_preg==0
			replace any_preg_preterm=0 if flag_37wk_initiators==1
			tab any_preg_preterm
			
			* Counts for the table	
			count if `outcome'!=.
			local total=`r(N)'
				
			count if  `outcome'==1  & any_preg_preterm==1
			local n_exp=`r(N)'
							
			count if  `outcome'==1  & any_preg_preterm==0
			local n_unexp=`r(N)'
									
			count if `outcome'!=. & any_preg_preterm==1
			local total_exp=`r(N)'
			local percent_exp=(`n_exp'/`total_exp')*100
							
			count if `outcome'!=. & any_preg_preterm==0
			local total_unexp=`r(N)'
			local percent_unexp=(`n_unexp'/`total_unexp')*100
			
		}
		
		else {
		    
			tab any_preg
			
			* Counts for the table	
			count if `outcome'!=.
			local total=`r(N)'
				
			count if  `outcome'==1  & any_preg==1
			local n_exp=`r(N)'
							
			count if  `outcome'==1  & any_preg==0
			local n_unexp=`r(N)'
									
			count if `outcome'!=. & any_preg==1
			local total_exp=`r(N)'
			local percent_exp=(`n_exp'/`total_exp')*100
							
			count if `outcome'!=. & any_preg==0
			local total_unexp=`r(N)'
			local percent_unexp=(`n_unexp'/`total_unexp')*100
				
		}
						
		file write `myhandle' "`outcome'" _tab %9.0fc (`total') _tab %7.0fc (`n_exp') ("/") %7.0fc (`total_exp') (" (") %4.2f (`percent_exp') (")") _tab %7.0fc (`n_unexp') ("/") %9.0fc (`total_unexp') (" (") %4.2f (`percent_unexp') (")")
								
		* Unadjusted
		
		if "`y'"=="preterm" {
			
			logistic `outcome' i.any_preg_preterm, vce(cluster mother_id) or
			
			local tot=`e(N)'
			lincom 1.any_preg_preterm, or 
			local minadjor=`r(estimate)'
			local minadjuci=`r(ub)'
			local minadjlci=`r(lb)'
			
		}
		
		else {
			
			logistic `outcome' i.any_preg, vce(cluster mother_id) or
			
			local tot=`e(N)'
			lincom 1.any_preg, or 
			local minadjor=`r(estimate)'
			local minadjuci=`r(ub)'
			local minadjlci=`r(lb)'
			
		}
						
		file write `myhandle' _tab %4.2f (`minadjor') (" (") %4.2f (`minadjlci') ("-") %4.2f (`minadjuci') (")") 
		
		* Adjusted for all covariates
		
		if "`y'"=="preterm" {
		
			logistic `outcome' i.any_preg birth_yr i.mother_age_cat i.mother_educ i.dispink5atbirth i.mother_birth_country_nonsverige i.mother_bmi_cat i.smoke_beg i.prev_sb_bin i.parity i.ap_12mo i.asm_12mo depression anxiety, vce(cluster mother_id) or
			
			local tot=`e(N)'
			lincom 1.any_preg_preterm, or 
			local minadjor=`r(estimate)'
			local minadjuci=`r(ub)'
			local minadjlci=`r(lb)'
			
		}
		
		else {
			
			logistic `outcome' i.any_preg birth_yr i.mother_age_cat i.mother_educ i.dispink5atbirth i.mother_birth_country_nonsverige i.mother_bmi_cat i.smoke_beg i.prev_sb_bin i.parity i.ap_12mo i.asm_12mo depression anxiety, vce(cluster mother_id) or
		
			local tot=`e(N)'
			lincom 1.any_preg, or 
			local minadjor=`r(estimate)'
			local minadjuci=`r(ub)'
			local minadjlci=`r(lb)'
			
		}

		file write `myhandle' _tab %4.2f (`minadjor') (" (") %4.2f (`minadjlci') ("-") %4.2f (`minadjuci') (")") _n
		
	}
	
	file close `myhandle'
	
********************************************************************************

* Stop logging, translate .smcl into .pdf and erase .smcl

	log close primary_analysis_in_sibs
	
	translate "$Logdir\2_analysis\20_primary analysis in sibs.smcl" "$Logdir\2_analysis\20_primary analysis in sibs.pdf", replace
	
	erase "$Logdir\2_analysis\20_primary analysis in sibs.smcl"
	
********************************************************************************/
