
/*******************************************************************************

	Generate table showing the sibling analysis models of antidepressant use during pregnancy and the outcomes of interest

	Author: Paul Madley-Dowd (amended by Flo Martin)

	Date: 16/10/2023

	Table generated by this script
	
		- sibling analysis.txt sibling analysis conditional logisitic regression models for each outcome
		
*******************************************************************************/

* Start logging

	log using "$Logdir\2_analysis\6_sibling analysis", name(sibling_analysis) replace
	
********************************************************************************
	
	ssc install tabcount
	
* Load in the analysis dataset
	
	use "$Deriveddir\maternal_analysis_dataset_reduced.dta", clear
	
	* Sibling analysis data management
	
	preserve
	
		sort mother_id start_date
		
		duplicates drop mother_id start_date, force // checking that there're no multiple pregnancies still in 
		
		by mother_id: egen _seq = seq()
		
		keep mother_id _seq any_preg
		
		reshape wide any_preg, i(mother_id) j(_seq)
		
		egen famavg_any_preg = rowmean(any_preg*) 
		
		keep mother_id famavg_any_preg
		
		save "$Tempdatadir\famavg_exposure.dta", replace
		
	restore 
	
	merge m:1 mother_id using "$Tempdatadir\famavg_exposure.dta", nogen
	
	* deviation from family averaged covariate 
	
	foreach covar in birth_yr mother_age_cat mother_educ dispink5atbirth mother_bmi_cat smoke_preg prev_sb_bin parity ap_12mo asm_12mo depression anxiety {
		
		preserve 
			
			sort mother_id start_date 
			duplicates drop mother_id start_date , force 
			by mother_id: egen _seq = seq() // generate identifier for each pregnacy a mother has - 11 babies maximum
			keep mother_id _seq `covar'	
			reshape wide `covar', i(mother_id) j(_seq)
			egen fa_`covar' = rowmean(`covar'*)  
			keep mother_id fa_`covar'
			save "$Tempdatadir\famavg_`covar'.dta", replace
		
		restore	
	
	}
	
	foreach covar in birth_yr mother_age_cat mother_educ dispink5atbirth mother_bmi_cat smoke_preg prev_sb_bin parity ap_12mo asm_12mo depression anxiety {	
		
		merge m:1 mother_id using "$Tempdatadir\famavg_`covar'.dta", nogen
		gen dev_`covar' = `covar'-fa_`covar'	
		
	}
	
	* Identify family size 
	
	sort mother_id start_date
	by mother_id: egen border = seq()
	by mother_id: egen famsize = max(border)
	
	preserve 
		
		keep if border <= 2 & famsize >=2
		keep mother_id border *any_preg
		drop famavg*
		reshape wide any_preg, i(mother_id) j(border) 

		gen firstsibexp_any1  = 1 if  any_preg1 == 1 & any_preg2 == 0 
		gen firstsibexp_any2  = firstsibexp_any1	
			
		gen secondsibexp_any1 = 1 if  any_preg1 == 0 & any_preg2 == 1 	
		gen secondsibexp_any2 = secondsibexp_any1		
	
		keep mother_id firstsibexp_* secondsibexp_*
		 
		reshape long firstsibexp_any secondsibexp_any, i(mother_id) j(border) 
		 
		save "$Tempdatadir\ord_exposure_expdisc", replace 
		 
	restore 
	
	merge m:1 mother_id border using "$Tempdatadir\ord_exposure_expdisc", nogen
	
	*drop if child_male==.
	
	* Family averaged covariates for shared covariates
	
	foreach covar in birth_yr smoke_preg prev_sb_bin ap_12mo asm_12mo depression anxiety {
	    
		gen `covar'_model = fa_`covar' - `covar'
		
	}
	
	* Preterm exposure - dropping new users after 36+6 weeks' gestation
	
	gen any_preg_preterm = 0 if any_preg==0 | flag_37wk_initiators==1
	replace any_preg_preterm = 1 if any_preg_preterm==.
	
	save "$Deriveddir\sibling_analysis.dta", replace
	
* Sibling analysis
	
	tempname myhandle	
	file open `myhandle' using "$Tabledir\sibling analysis.txt", write replace
	
	file write `myhandle' "Outcome" _tab "Total" _tab "Exposed n/N" _tab "Unexposed n/N" _tab "OR" _tab "aOR*" _n
	
	use "$Deriveddir\sibling_analysis.dta", clear
	
	gen cc = 1 if birth_yr!=. & mother_age_cat!=. & mother_educ!=. & dispink5atbirth!=. & mother_birth_country_nonsverige!=. & mother_bmi_cat!=. & smoke_preg!=. & prev_sb_bin!=. & parity!=. & ap_12mo!=. & asm_12mo!=. & depression!=. & anxiety!=.
	keep if cc==1
	
	foreach outcome in stillborn neonatal_death preterm postterm sga_pct lga_pct apgar5_bin {
	    
		* Use the preterm exposure variable for the preterm models (no new users after 37 weeks' in the exposed group)
		local y="`outcome'"
		if "`y'"=="preterm" {
		    
			capture noisily clogit `outcome' i.any_preg_preterm, group(mother_id) or
		
			tabcount any_preg_preterm `outcome' if e(sample), v1(0/1) v2(0/1) matrix(exposed)
			local total=`e(N)'
			local unexp_lb=exposed[1,1]
			local unexp_sb=exposed[1,2]
			local unexp_n=`unexp_lb'+`unexp_sb'
			local exp_lb=exposed[2,1]
			local exp_sb=exposed[2,2]
			local exp_n=`exp_lb'+`exp_sb'
			
			disp `unexp_sb' 
			
			file write `myhandle' "`outcome'" _tab %7.0fc (`total') _tab %6.0fc (`exp_sb') ("/") %6.0fc (`exp_n') _tab %6.0fc (`unexp_sb') ("/") %6.0fc (`unexp_n')
			
			lincom 1.any_preg_preterm, or
			local minadjor=`r(estimate)'
			local minadjuci=`r(ub)'
			local minadjlci=`r(lb)'
			
			file write `myhandle' _tab %4.2f (`minadjor') (" (") %4.2f (`minadjlci') ("-") %4.2f (`minadjuci') (")")
			
			capture noisily clogit `outcome' i.any_preg_preterm birth_yr_model i.mother_age_cat i.mother_educ i.dispink5atbirth i.mother_bmi_cat i.smoke_preg i.parity ap_12mo_model asm_12mo_model depression_model anxiety_model, group(mother_id) or
			
			lincom 1.any_preg_preterm, or
			local minadjor=`r(estimate)'
			local minadjuci=`r(ub)'
			local minadjlci=`r(lb)'
			
			file write `myhandle' _tab %4.2f (`minadjor') (" (") %4.2f (`minadjlci') ("-") %4.2f (`minadjuci') (")") _n
			
		}
		
		else {
		    
			capture noisily clogit `outcome' i.any_preg, group(mother_id) or
		
			tabcount any_preg `outcome' if e(sample), v1(0/1) v2(0/1) matrix(exposed)
			
			local total=`e(N)'
			local unexp_lb=exposed[1,1]
			local unexp_sb=exposed[1,2]
			local unexp_n=`unexp_lb'+`unexp_sb'
			local exp_lb=exposed[2,1]
			local exp_sb=exposed[2,2]
			local exp_n=`exp_lb'+`exp_sb'
			
			disp `unexp_sb' 
			
			file write `myhandle' "`outcome'" _tab %7.0fc (`total') _tab %6.0fc (`exp_sb') ("/") %6.0fc (`exp_n') _tab %6.0fc (`unexp_sb') ("/") %7.0fc (`unexp_n')
			
			lincom 1.any_preg, or
			local minadjor=`r(estimate)'
			local minadjuci=`r(ub)'
			local minadjlci=`r(lb)'
			
			file write `myhandle' _tab %4.2f (`minadjor') (" (") %4.2f (`minadjlci') ("-") %4.2f (`minadjuci') (")")
			
			capture noisily clogit `outcome' i.any_preg birth_yr_model i.mother_age_cat i.mother_educ i.dispink5atbirth i.mother_bmi_cat i.smoke_preg i.parity ap_12mo_model asm_12mo_model depression_model anxiety_model, group(mother_id) or
			
			lincom 1.any_preg, or
			local minadjor=`r(estimate)'
			local minadjuci=`r(ub)'
			local minadjlci=`r(lb)'
			
			file write `myhandle' _tab %4.2f (`minadjor') (" (") %4.2f (`minadjlci') ("-") %4.2f (`minadjuci') (")") _n
			
		}
	
	}

********************************************************************************

* Erase unnecessary datasets

	erase "$Tempdatadir\famavg_exposure.dta"
	
	foreach covar in birth_yr mother_age_cat mother_educ parity ap_12mo asm_12mo depression anxiety {
	    
		erase "$Tempdatadir\famavg_`covar'.dta"
		
	}
	
	erase "$Tempdatadir\ord_exposure_expdisc.dta"

* Stop logging, translate .smcl into .pdf and erase .smcl

	log close sibling_analysis
	
	translate "$Logdir\2_analysis\6_sibling analysis.smcl" "$Logdir\2_analysis\6_sibling analysis.pdf", replace
	
	erase "$Logdir\2_analysis\6_sibling analysis.smcl"
	
********************************************************************************