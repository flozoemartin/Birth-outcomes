
/*******************************************************************************

	Generate table for trimester-specific analysis 

	Author: Flo Martin

	Date: 26/09/2023

	Table generated by this script
	
		- trimester specific analysis.txt logisitic regression models for each outcome by whether or not they were exposed during each trimester or not
		
*******************************************************************************/

* Start logging

	log using "$Logdir\2_analysis\7_trimester specific analysis", name(trimester_specific_analysis) replace
	
********************************************************************************	

	tempname myhandle	
	file open `myhandle' using "$Tabledir\trimester-specific analysis.txt", write replace
	
	file write `myhandle' "Outcome" _tab "Exposed in T1 n/N (%)" _tab "Trimester one exposure vs no exposure aOR*" _tab "Exposed in T2 n/N (%)" _tab "Trimester two exposure vs no exposure aOR*" _tab "Exposed in T3 n/N (%)" _tab "Trimester three exposure vs no exposure aOR*" _n

	use "$Deriveddir\maternal_analysis_dataset_reduced.dta", clear
	
	gen cc = 1 if birth_yr!=. & mother_age_cat!=. & mother_educ!=. & dispink5atbirth!=. & mother_birth_country_nonsverige!=. & mother_bmi_cat!=. & smoke_beg!=. & prev_sb_bin!=. & parity!=. & ap_12mo!=. & asm_12mo!=. & depression!=. & anxiety!=. 
	
	keep if cc==1
	
	replace t3=. if gest_age_wks<27
	
	foreach outcome in stillborn neonatal_death preterm postterm sga_pct lga_pct apgar5_bin {
		
		file write `myhandle' "`outcome'"
		
		* Recode incident users after 37 weeks' unexposed for the preterm delivery modesls
		local y="`outcome'"
		if "`y'"=="preterm" {
		    
			replace t3=0 if flag_37wk_initiators==1
			tab t3
			
		}
		
		else {
		    
			tab any_preg
			
		}
		
		forvalues x=1/3 {
		    
			count if t`x'==1 & `outcome'==1
			local exp_n=`r(N)'
			
			count if t`x'==1 & `outcome'!=.
			local total=`r(N)'
			
			local pct=((`exp_n')/(`total'))*100
			
			file write `myhandle' _tab %5.0fc (`exp_n') ("/") %7.0fc (`total') (" (") %4.2f (`pct') (")")
		
			* Adjusted for all covariates
		
			logistic `outcome' i.t`x' birth_yr i.mother_age_cat i.mother_educ i.dispink5atbirth i.mother_birth_country_nonsverige i.mother_bmi_cat i.smoke_beg i.prev_sb_bin i.parity i.ap_12mo i.asm_12mo depression anxiety, vce(cluster mother_id) or
		
			local tot=`e(N)'
			lincom 1.t`x', or 
			local minadjor=`r(estimate)'
			local minadjuci=`r(ub)'
			local minadjlci=`r(lb)'

			file write `myhandle' _tab %4.2f (`minadjor') (" (") %4.2f (`minadjlci') ("-") %4.2f (`minadjuci') (")")
		
		}
		
		file write `myhandle' _n
	}
	
********************************************************************************

* Stop logging, translate .smcl into .pdf and erase .smcl

	log close trimester_specific_analysis
	
	translate "$Logdir\2_analysis\7_trimester specific analysis.smcl" "$Logdir\2_analysis\7_trimester specific analysis.pdf", replace
	
	erase "$Logdir\2_analysis\7_trimester specific analysis.smcl"
	
********************************************************************************