*************************************************************************************

* Creating and cleaning the outcome variables for the birth outcomes

* Author: Flo Martin

* Date: 10/04/2024

*************************************************************************************

* Datasets generated by this do-file

* 	- $Datadir\primary_analysis_dataset.dta

*************************************************************************************

* Start logging

	log using "$Logdir\1_cleaning\1_cleaning outcomes", name(cleaning_outcomes) replace
	
*************************************************************************************

* Data preparation

	* Load in the singleton pregnancy dataset created in "1_birweit and gestat.do"

	use "$Deriveddir\derived_data\pregnancy_cohort_final.dta", clear
	count
	
	* Updated outcome - just deliveries
	rename updated_outcome outcome
	tab outcome
	tab outcome if hes_apc_e==1
	
*************************************************************************************
	
* Apply eligibility criteria

	tab outcome
	keep if outcome<=2 // 506,997 dropped
	
	count // 738,149
	
	* Drop deliveries from <22 weeks
	tab gestdays, m
	count if gestdays<154
	drop if gestdays<154 // 3,306 dropped

	* Drop multiple births
	tab multiple_ev // 6,254
	drop if multiple_ev==1
	
	count if mblbabies>1 & mblbabies!=. // 6,039
	drop if mblbabies>1 & mblbabies!=.
	
	tab multi_flag // 681
	drop if multi_flag==1
	
	count // 725,129
	
	gen gest_age_wks = floor(gestdays/7) 
	tab gest_age_wks // gestational weeks range from 22 (for matching with Nordic analyses) to 46
	
	* Drop unknown outcomes (in this case uncertain delivery episodes)
	tab outcome, nol
	tab outcome
	
*************************************************************************************
	
* OUTCOMES

	tab outcome
	
	* Stillbirth
	gen stillborn = 1 if outcome==2
	replace stillborn = 0 if outcome==1
	tab stillborn
	
	* Gestational age at delivery
	gen term_cat = 0 if gestdays<259
	replace term_cat = 1 if gestdays>=259 & gestdays<294 & gestdays!=.
	replace term_cat = 2 if gestdays>=294 & gestdays!=.
	
	label define term_cat_lb 0"Preterm delivery" 1"Term delivery" 2"Post-term delivery"
	label values term_cat term_cat_lb
		
	tab term_cat, m
	
	* Preterm and postterm delivery variables for logistic regressions
	gen preterm = 1 if term_cat==0
	replace preterm = 0 if term_cat!=0
	tab preterm, m
		
	gen postterm = 1 if term_cat==2
	replace postterm = 0 if term_cat==1
	tab postterm, m
		
	gen postterm_sens = 1 if gestdays>=287 & gestdays!=.
	replace postterm_sens = 0 if gestdays>=259 & gestdays<287 & gestdays!=.
	
	* Classes of preterm delivery - moderate-to-late preterm (32-37 weeks'), very preterm (28-32 weeks') and extremely preterm (<28 weeks')
		gen preterm_class = 0 if preterm==0
		replace preterm_class = 1 if preterm==1
		replace preterm_class = 2 if gestdays<224
		replace preterm_class = 3 if gestdays<196
		tab preterm_class, m
	
	* Moderate-to-late preterm
		gen modpreterm = 1 if preterm_class==1
		replace modpreterm = 0 if preterm_class==0 
		tab preterm_class modpreterm, m
			
	* Very preterm
		gen verypreterm = 1 if preterm_class==2
		replace verypreterm = 0 if preterm_class!=3 & preterm_class!=2 & preterm_class!=.
		tab preterm_class verypreterm, m
			
	* Extremely preterm
		gen expreterm = 1 if preterm_class==3
		replace expreterm = 0 if preterm_class!=3 & preterm_class!=.
		tab preterm_class expreterm, m
	
********************************************************************************

* Birthweight

		rename birweit birth_weight
		rename sex child_male
		recode child_male 2=0
	
		sum birth_weight, det // 1 - 7050 grams
		br if birth_weight==0 // n=0 with birthweight of 0
		count if birth_weight==. // n=448,918 with missing birthweight
		
		tab child_male, m // 457,559 without a sex therefore won't have a birthweight Z-score
		
		summ gest_age_wks, det
		local wk=`r(max)'
		
		* Save the dataset

		save "$Tempdatadir\eligble_sample_prebw.dta", replace
		
		* Generate the centiles
		
		forvalues x=22/`wk' {
			foreach sex in 0 1 {

				use "$Tempdatadir\eligble_sample_prebw.dta", clear
				
				drop if birth_weight==. | birth_weight==0
				count
				
				tab gest_age_wks
				
				keep if gest_age_wks==`x'
				keep if child_male==`sex'
			
				if _N>1 {
			
					egen zbweight = std(birth_weight)
					egen bweight_10 = pctile(birth_weight), p(10)
					egen bweight_90 = pctile(birth_weight), p(90)
				
				}
				
				else if _N==1 {
				
					egen zbweight = std(birth_weight)
					egen bweight_10 = pctile(birth_weight), p(10)
					egen bweight_90 = pctile(birth_weight), p(90)
				
				}
			
				save "$Tempdatadir\birthweight_`x'wks_`sex'.dta", replace
			
			}
		}
	
	use "$Tempdatadir\birthweight_22wks_0.dta", clear
	append using "$Tempdatadir\birthweight_22wks_1.dta"
	
	forvalues x=22/`wk' {
		foreach sex in 0 1 {
		
			append using "$Tempdatadir\birthweight_`x'wks_`sex'.dta"
		
		}
	}
	
	duplicates drop
	
	twoway /*
	*/ histogram zbweight if child_male==0, blcolor(red) bfcolor(none) fraction || /*
	*/ histogram zbweight if child_male==1, bfcolor(none) blcolor(blue) fraction legend(order(0 "Female" 1 "Male"))
	
	/* Small for gestational age (SGA) is defined as birth weight of less than 10th percentile for gestational age - for these data I have created percentiles within each week of gestational age
 https://www.ncbi.nlm.nih.gov/books/NBK563247/#:~:text=Small%20for%20gestational%20age%20(SGA,the%20neonatal%20period%20and%20beyond. */

	gen sga_z = 1 if zbweight < invnormal(0.1)
	replace sga_z = 0 if zbweight >= invnormal(0.1) & zbweight<=invnormal(0.9) & zbweight!=.
	
	tab sga_z
	label variable sga_z"Small for gestational age - <10th percentile in birthweight Z-score for gestational age (weeks)"
	
	gen sga_pct = 1 if birth_weight < bweight_10
	replace sga_pct = 0 if birth_weight >= bweight_10 & birth_weight<=bweight_90 & birth_weight!=.
	
	tab sga_pct
	label variable sga_pct"Small for gestational age - <10th percentile in birthweight for gestational age (weeks)"
	
/* Large for gestational age (LGA) is defined as birth weight of more than 90th percentile for gestational age - for these data I have created percentiles within each week of gestational agehttps://www.esneft.nhs.uk/leaflet/large-for-gestational-age-babies-information-for-parents/#:~:text=What%20is%20considered%20a%20large,babies%20are%20identified%20as%20LGA. */

	gen lga_z = 1 if zbweight > invnormal(0.9) & zbweight!=. 
	replace lga_z = 0 if zbweight <= invnormal(0.9) & sga_z!=1
	
	tab lga_z
	label variable lga_z"Large for gestational age - >90th percentile in birthweight Z-score gestational age (weeks)"
	
	gen lga_pct = 1 if birth_weight > bweight_90 & birth_weight!=. 
	replace lga_pct = 0 if birth_weight <= bweight_90 & sga_pct!=1
	
	tab lga_pct
	label variable lga_pct"Large for gestational age - >90th percentile in birthweight gestational age (weeks)"
	
	gen aga_z = 1 if lga_z!=1 & sga_z!=1
	replace aga_z = 0 if lga_z==1 | sga_z==1
	label variable aga_z"Adequate for gestational age - <=90th  or >=10th percentile in birthweight Z-score for gestational age (weeks)"
	
	gen aga_pct = 1 if lga_pct!=1 & sga_pct!=1
	replace aga_pct = 0 if lga_pct==1 | sga_pct==1
	label variable aga_pct"Adequate for gestational age - <=90th  or >=10th percentile in birthweight for gestational age (weeks)"
	
********************************************************************************

* Externally validate size for gestational age against the Intergrowth-21 standard and the New Swedish standard
		
	preserve 	
		
		do "$Dodir\1_data management\1a_externally validating size for ga intergrowth.do"
		do "$Dodir\1_data management\1b_externally validating size for ga swedish.do"
		
	restore

	merge 1:1 patid pregid using "$Datadir\externally_valid_sizeforga_intergrowth.dta", nogen
	merge 1:1 patid pregid using "$Datadir\externally_valid_sizeforga_swedish.dta", nogen
	
	merge 1:1 patid pregid using "$Tempdatadir\eligble_sample_prebw.dta", update replace nogen
	
	count
	
********************************************************************************

	* Stillbirth
	tab stillborn, m
	
	* Neonatal death - not available in CPRD
	
	* Preterm delivery
	tab preterm, m
	
		* Sensitivity analysis
		tab preterm_ev
		recode preterm_ev 9=.
		
		gen preterm_hes =.
		replace preterm_hes = 1 if gestat>=22 & gestat<37
		replace preterm_hes = 0 if gestat>=37 & gestat!=.
		tab preterm_hes
		
	* Post-term delivery
	tab postterm, m
	
		* Sensitivity analysis
		tab postterm_ev
		recode postterm_ev 9=.
		
		gen postterm_hes =.
		replace postterm_hes = 0 if gestat>=37 & gestat<42
		replace postterm_hes = 1 if gestat>=42 & gestat!=.
		tab postterm_hes
		
		tab postterm_sens
		
	* Size for gestational age
	gen weit_pct = 0 if sga_pct==1
	replace weit_pct = 1 if aga_pct==1
	replace weit_pct = 2 if lga_pct==1
	tab weit_pct
	
	label define weit_pct_lb 0"SGA" 1"AGA" 2"LGA"
	label values weit_pct weit_pct_lb
	tab weit_pct
	
	* Apgar score - not available in CPRD
	
********************************************************************************

	* Save eligible sample for the analysis dataset
	
	save "$Datadir\eligible_outcomes.dta", replace
	
*********************************************************************************

* Delete unnecessary datasets

	erase "$Tempdatadir\pre_dates_new_users.dta"

* Stop logging, translate .smcl into .pdf and erase .smcl

	log close cleaning_outcomes
	
	translate "$Logdir\1_cleaning\1_cleaning outcomes.smcl" "$Logdir\1_cleaning\1_cleaning outcomes.pdf", replace
	
	erase "$Logdir\1_cleaning\1_cleaning outcomes.smcl"
	
*********************************************************************************
