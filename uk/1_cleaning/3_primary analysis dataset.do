*************************************************************************************

* Creating the analysis dataset for the analysis of birth outcomes in antidepressant use during pregnancy project

* Author: Flo Martin

* Date: 30/08/2024

*************************************************************************************

* Datasets generated by this do-file

* 	- $Datadir\primary_analysis_dataset.dta

*************************************************************************************

* Start logging

	log using "$Logdir\1_cleaning\3_primary analysis dataset", name(primary_analysis_dataset) replace
	
*************************************************************************************

* Data preparation

	* Load in the singleton pregnancy dataset created in "1_birweit and gestat.do"

	use "$Datadir\eligible_outcomes.dta", clear
	count
	
	* Updated outcome - just deliveries
	tab outcome if hes_apc_e==1
	
	* Add in antidepressant exposure information
	merge 1:1 patid pregid using "$Deriveddir\exposure\pregnancy_cohort_exposure.dta", keep(master match) nogen
	count

	* Merge on covariate information
	merge 1:1 patid pregid using "$Deriveddir\covariates\pregnancy_cohort_covariates.dta",  keep(match master) nogen
	merge 1:1 patid pregid using "$Deriveddir\covariates\smoking_preg_final.dta",  keep(match master) nogen
	
	* Indications
	
		* Binary depression from primary care
	merge 1:1 patid pregid using "$Deriveddir\indications\all_depression_read.dta", keep(1 3) nogen
	count
	
		* Binary anxiety from primary care
	merge 1:1 patid pregid using "$Deriveddir\indications\all_anxiety_read.dta", keep(1 3) nogen
	count
	
		* Binary depression from secondary care
	merge 1:1 patid pregid using "$Deriveddir\indications\all_depression_hes.dta", keep(1 3) nogen
	count
	
		* Binary anxiety from secondary care
	merge 1:1 patid pregid using "$Deriveddir\indications\all_anxiety_hes.dta", keep(1 3) nogen
	count
	
		* Binary other AD indications from primary care
	merge 1:1 patid pregid using "$Deriveddir\indications\all_other_indics_read.dta", keep(1 3) nogen
	count
	
		* Binary other AD indications from secondary care
	merge 1:1 patid pregid using "$Deriveddir\indications\all_other_indics_hes.dta", keep(1 3) nogen
	count	

	* Merge on tod, lcd, deathdate_num
	merge m:1 patid using "$Deriveddir\formatted_cprd_data\All_Patient.dta", keep(3) keepusing(tod_num crd_num) nogen
	tostring patid, gen(patid_s) format(%12.0f)
	merge m:1 pracid using "$Deriveddir\formatted_cprd_data\All_Practice.dta", nogen keep(match) keepusing(lcd_num uts_num)
	codebook pregid
	
	tab hes_apc_e
	tab baby_hes_apc_e

	count
	
	summ pregnum
	local max=`r(max)'
	
********************************************************************************

* COVARIATES

	* Maternal age

	summ matage		// 9 - 48
	drop matage_cat	
	
	gen mother_age_cat = 0 if matage<20
	replace mother_age_cat = 1 if matage>=20 & matage<25
	replace mother_age_cat = 2 if matage>=25 & matage<30
	replace mother_age_cat = 3 if matage>=30 & matage<35
	replace mother_age_cat = 4 if matage>=35 & matage<40
	replace mother_age_cat = 5 if matage>=40 & matage<45
	replace mother_age_cat = 6 if matage>=45 & matage!=.
	
	label define age_lb 0"<20" 1"20-24" 2"25-29" 3"30-34" 4"35-40" 5"40-45" 6"45+"
	label values mother_age_cat age_lb
	
	tab mother_age_cat
	
	* Maternal BMI
	
	tab bmi_cat
	tab bmi_cat, nol
	recode bmi_cat 4=.
	rename bmi_cat mother_bmi_cat
	
	* Maternal ethnicity 
	
	tab eth5
	tab eth5, nol
	recode eth5 5=. // 31% missing - not include in primary adjustment set?
	rename eth5 mother_ethn
	
	* IMD
	
	tab imd_practice, m
	
	* Pregnancy year
	
	gen birth_yr=year(pregend_num)
	egen birth_yr_cat=cut(birth_yr), at(1995 2000 2005 2010 2016 2020)
	tab birth_yr_cat
	recode birth_yr_cat 1995=1 2000=2 2005=3 2010=4 2016=5
	lab define birth_yr_cat 1 "1996-1999" 2 "2000-2004" 3 "2005-2009" 4 "2010-2016" 5 "2016-2019"
	lab val birth_yr_cat birth_yr_cat
	tab birth_yr birth_yr_cat
	
	* History of loss
	
	tab grav_hist_sa, m
	tab grav_hist_sb, m
	
	gen hist_loss = 1 if grav_hist_sa==1 | grav_hist_sb==1 | grav_hist_otherloss==1 /*Not including terminations*/
	replace hist_loss = 0 if hist_loss!=1
	
	tab hist_loss
	
	* Parity
	
	rename parity parity_num
	drop parity_cat
	
	gen parity = 0 if parity_num==0
	replace parity = 1 if parity_num==1
	replace parity = 2 if parity_num==2
	replace parity = 3 if parity_num==3
	replace parity = 4 if parity_num>=4 & parity_num!=.
	
	label drop parity_lb
	label define parity_lb 4"4+"
	label values parity parity_lb
	tab parity, m
	
	* Indication
	
	* Depression ever before the start of pregnancy
	
	rename depression depression_read
	gen depression =1 if depression_read==1 | depression_hes==1 
	replace depression = 0 if depression==.
	tab depression any_preg, col
	
	label variable depression"Depression noted in CPRD/HES ever before the start of pregnancy"
	
	replace depression_ever = 1 if depression_ever_hes==1
	replace depression_12mo = 1 if depression_12mo_hes==1
	replace depression_preg = 1 if depression_preg_hes==1
	
	* Anxiety ever before the start of pregnancy

	rename anxiety anxiety_read
	gen anxiety =1 if anxiety_read==1 | anxiety_hes==1 
	replace anxiety = 0 if anxiety==.
	tab anxiety any_preg, col
	
	label variable anxiety"Anxiety noted in CPRD/HES ever before the start of pregnancy"
	
	replace anxiety_ever = 1 if anxiety_ever_hes==1
	replace anxiety_12mo = 1 if anxiety_12mo_hes==1
	replace anxiety_preg = 1 if anxiety_preg_hes==1
	
	* Other affective / mood disorders before the start of pregnancy
	
	rename mood mood_read
	gen mood = 1 if mood_read==1 | affective_hes==1 
	replace mood = 0 if mood==.
	tab mood any_preg, col
	
	label variable mood"Other mood disorders with a depressive element noted in CPRD/HES ever before the start of pregnancy"
	
	replace mood_ever = 1 if affective_ever_hes==1
	replace mood_12mo = 1 if affective_12mo_hes==1
	replace mood_preg = 1 if affective_preg_hes==1
	
	* Eating disorders
	
	rename ed ed_read
	gen ed = 1 if ed_read==1 | ed_hes==1 
	replace ed = 0 if ed==.
	tab ed any_preg, col
	
	label variable ed"Eating disorder noted in CPRD/HES ever before the start of pregnancy"
	
	replace ed_ever = 1 if ed_ever_hes==1
	replace ed_12mo = 1 if ed_12mo_hes==1
	replace ed_preg = 1 if ed_preg_hes==1
	
	* Pain
	
	rename pain pain_read
	gen pain = 1 if pain_read==1 | pain_hes==1 
	replace pain = 0 if pain==.
	tab pain any_preg, col
	
	label variable pain"Pain noted in CPRD/HES ever before the start of pregnancy"
	
	replace pain_ever = 1 if pain_ever_hes==1
	replace pain_12mo = 1 if pain_12mo_hes==1
	replace pain_preg = 1 if pain_preg_hes==1
	
	* Tension-type headache
	
	rename headache headache_read
	gen headache = 1 if headache_read==1 | tt_headache_hes==1 
	replace headache = 0 if headache==.
	tab headache any_preg, col
	
	label variable headache"Tension-type headache noted in CPRD/HES ever before the start of pregnancy"
	
	replace headache_ever = 1 if tt_headache_ever_hes==1
	replace headache_12mo = 1 if tt_headache_12mo_hes==1
	replace headache_preg = 1 if tt_headache_preg_hes==1
	
	* Diabetic neuropathy
	
	rename dn dn_read
	gen dn = 1 if dn_read==1 | dn_hes==1 
	replace dn = 0 if dn==.
	tab dn any_preg, col
	
	label variable dn"Diabetic neuropathy noted in CPRD/HES ever before the start of pregnancy"
	
	replace dn_ever = 1 if dn_ever_hes==1
	replace dn_12mo = 1 if dn_12mo_hes==1
	replace dn_preg = 1 if dn_preg_hes==1
	
	* Migraine prophylaxis
	
	rename migraine migraine_read
	gen migraine = 1 if migraine_read==1 | migraine_hes==1 
	replace migraine = 0 if migraine==.
	tab migraine any_preg, col
	
	label variable migraine"Migraine prophylaxis noted in CPRD/HES ever before the start of pregnancy"
	
	replace migraine_ever = 1 if migraine_ever_hes==1
	replace migraine_12mo = 1 if migraine_12mo_hes==1
	replace migraine_preg = 1 if migraine_preg_hes==1
	
	* Stress incontinence
	
	rename incont incont_read
	gen incont = 1 if incont_read==1 | stress_incont_hes==1 
	replace incont = 0 if incont==.
	tab incont any_preg, col
	
	label variable incont"Stress incontinence noted in CPRD/HES ever before the start of pregnancy"
	
	replace incont_ever = 1 if stress_incont_ever_hes==1
	replace incont_12mo = 1 if stress_incont_12mo_hes==1
	replace incont_preg = 1 if stress_incont_preg_hes==1
	
	gen other_indic = 1 if incont==1 | migraine==1 | dn==1 | headache==1 | pain==1 | mood==1 | ed==1
	
	* Label indication variables

	tab depression
	tab anxiety
	tab other_indic
	recode other_indic .=0

	label variable other_indic "Other indications for antidepressants ever before the start of pregnancy"
	
	* Folic acid 5mg
	
	gen folate_before = 1 if folic_prepreg0==1 | folic_prepreg1==1
	replace folate_before = 0 if folate_before==.
	
	tab folate_before
	
	* Anti-seizure medications
	
	tab moodstabs_prepreg
	rename moodstabs_prepreg asm_12mo
	
	* Antipsychotics
	
	tab antipsychotics_prepreg
	rename antipsychotics_prepreg ap_12mo
	
	* Smoking
	
	tab smokstatus
	recode smokstatus 3=. // 4.4% missing
	tab smokstatus, m
	
	tab smoke_preg, nol m
	recode smoke_preg 1=0
	recode smoke_preg 2=1
	
	label define smok_lb 0"Non-smoker" 1"Smoker"
	label values smoke_preg smok_lb

********************************************************************************

* EXPOSURE

	* For primary logistic regression models
	tab any_preg
	
	* Specific drug analysis
	tab specific_drug_analysis
	rename specific_drug_analysis drug_preg

* Exposure status

	replace any_a = 0 if any_a!=1
	replace any_b = 0 if any_b!=1
	replace any_c = 0 if any_c!=1
	replace any_o = 0 if any_o!=1
	
	* Class
	
	tab class_t1
	tab class_t2
	tab class_t3
	
	* Dose
	tab highest_dose_t1
	tab highest_dose_t2
	tab highest_dose_t3
	
	* Primary analysis - exposed vs unexposed
	
	gen cf_unexposed=1 if any_a==1 | any_b==1 | any_c==1
	replace cf_unexposed=0 if cf_unexposed!=1
	
	tab cf_unexposed // 50,879 exposed to antidepressants during pregnancy
	
	* Secondary analysis - exposed prevalent vs unexposed and exposed incident vs unexposed
	
	* Incident users in pregnancy
	
	gen cf_unexp_incid=.
	replace cf_unexp_incid=1 if any_a==1 & any_o==0 & presc_startdate_num1a>=pregstart_num & presc_startdate_num1a<=pregstart_num+91
	replace cf_unexp_incid=2 if any_b==1 & any_a==0 & any_o==0 & presc_startdate_num1b>pregstart_num+91 & presc_startdate_num1b<=pregstart_num+196
	replace cf_unexp_incid=3 if any_c==1 & any_b==0 & any_a==0 & any_o==0 & presc_startdate_num1c>pregstart_num+196 & presc_startdate_num1c<pregend_num
	replace cf_unexp_incid=0 if any_a==0 & any_b==0 & any_c==0
	
	tab cf_unexp_incid // 4,301 incident users during pregnancy
	
	* Prevalent users exposed in trimester 1
	
	gen cf_unexp_prev=1 if cf_unexposed==1 & cf_unexp_incid!=1
	replace cf_unexp_prev=0 if cf_unexp_prev!=1
	
	tab cf_unexp_prev // 16,283 prevalent users during pregnancy
	
	* Secondary analysis - negative control-type analysis - exposed vs unexposed discontinuers 3 months pre-preg
	
	gen cf_discont = 1 if cf_unexposed==1
	replace cf_discont = 0 if any_prepreg==1 & cf_unexposed==0
	tab cf_discont // probably underpowered comparator with discontinuers in the 3 months before pregnancy? Better to use any use in the 12 months before? Wood et al. use 6 months
	
  * Trimester-specific analysis
	
	gen trim_cat = 0 if any_preg==0
	replace trim_cat = 1 if any_a==1
	replace trim_cat = 2 if any_b==1
	replace trim_cat = 3 if any_c==1
	
	tab trim_cat
	
	* Sensitivity analyses
	
		* Exposed vs unexposed, indication ever and exposed vs unexposed, depression/anxiety in the 12 months prior to pregnancy

	gen cf_unexp_indicated_ever=1 if cf_unexposed==1
	replace cf_unexp_indicated_ever=0 if cf_unexposed==0 & (depression==1 | anxiety==1 | other_indic==1) /*Unexposed, but indicated*/
	
	tab cf_unexp_indicated_ever // 71,990 controls indicated ever
	
	gen cf_unexp_indicated_12mo=1 if cf_unexposed==1
	replace cf_unexp_indicated_12mo=0 if cf_unexposed==0 & (depression_12mo==1 | anxiety_12mo==1 | other_indic_12mo==1) /*Unexposed, but indicated in 12 months pre-preg*/
	
	tab cf_unexp_indicated_12mo // 19,590 controls indicated in 12 months pre-preg*/
	
	* Exposed prevalent (at least 2 prescriptions in the 12 months prior to pregnancy) vs unexposed and exposed incident (no prescriptions in the 12 months prior to pregnancy) vs unexposed
	
	gen cf_unexp_prev_sens=1 if cf_unexp_prev==1 & any_o==1 & (any_l==1 | any_m==1 | any_n==1)
	replace cf_unexp_prev_sens=0 if cf_unexposed==0
	
	tab cf_unexp_prev_sens // 13,036 with at least two prescriptions pre-preg
	
	gen cf_unexp_incid_sens=1 if cf_unexp_incid==1 & any_prepreg==0
	replace cf_unexp_incid_sens=0 if cf_unexposed==0
	
	tab cf_unexp_incid_sens // 3,043 incident users with no use in 12 months pre-preg
	
********************************************************************************

* SENSITIVITY ANALYSES - need to create a flag for those with a linked delivery/birth record so the denominator is right (out of those with linked data rather than everybody in the preg reg where missing data will look like way more of an issue)

	* Re-run analyses among just those with gestat rather than gestat plus pregnancy register dates
	
	sum gestat
	tab preterm_hes
	tab postterm_hes

	* Logisitic regression of antidepressant use during pregnancy on "unfeasible gestational length" (>42) to see if antidepressant use predicts this (& would therefore be problematic changing these to missing)
	
	sum gestdays 
	gen unfeas_gestdays = 1 if (gestdays>(44*7) | gestdays<(24*7)) & gestdays!=.
	replace unfeas_gestdays = 0 if unfeas_gestdays!=1
	tab unfeas_gestdays
	
	* Re-run analyses using 'preterm_ev' and 'postterm_ev' as the outcome
	
	tab preterm_ev
	tab postterm_ev
	
	* Re-run among outcomes that weren't updated
	
	tab outcome_updated_flag
	
	* Induction
	tab delonset, m
	tab delonset, nol
	
	recode delonset 8=.
	
	gen spont_labour = 1 if delonset==1
	replace spont_labour = 0 if delonset!=1 & delonset!=.
	
	tab spont_labour
	
		* Preterm induction sensitivity analysis
		gen spont_preterm = 1 if spont_labour==1 & preterm==1
		replace spont_preterm = 0 if preterm==0 & spont_labour!=.
		tab spont_preterm
		
		gen induc_preterm = 1 if spont_labour==0 & preterm==1
		replace induc_preterm = 0 if preterm==0 & spont_labour!=.
		tab induc_preterm
	
********************************************************************************

* Save dataset ready for time updated exposure variables	
	
	save "$Tempdatadir\pre_dates_new_users.dta", replace
	
********************************************************************************

* TIME UPDATED EXPOSURE
 	
* Get data to define time-update exposure status

	forvalues y=1/3 {
		
		* Trimester `y'
		use "$Tempdatadir\pre_dates_new_users.dta", clear
		
		gen tri`y'_new_user = 1 if cf_unexp_incid==`y' // not in the 3 months before pregnancy but in T1
		label variable tri`y'_new_user"Flags new antidepressant use in T`y'"
		
		save "$Tempdatadir\predates_t`y'_new_users.dta", replace
		
	* Get dates of 1st trimester prescription for new users

		bysort patid (pregnum_new): egen seq=seq()
		summ seq // max number of pregnancies within the study period is 14
		local max = `r(max)'
		
		forvalues x=1/`max' {
			
			*duplicates drop patid, force - do I want to do this? Some patients initiate ADs in T1 for multiple pregnancies
			use "$Tempdatadir\predates_t`y'_new_users.dta", clear
			keep if tri`y'_new_user==1
			keep if pregnum_new==`x'
			merge 1:m patid using "$Patternsdir\patterns_in_pregnancy_clean_withclasses.dta", keep(master match) nogen
			
			if _N>0 & `y'==1 {
			
				keep patid pregid pregstart_num any_o any_a presc_startdate_num1a cf_unexp_incid
				sort patid pregid presc_startdate_num1a
				br
				gen flag_1st_trim_presc=1 if presc_startdate_num1a>=pregstart_num & presc_startdate_num1a<pregstart_num+91
				keep if flag_1st==1
				bysort pregid (presc_startdate_num1a): keep if _n==1
				codebook pregid
				rename presc_startdate_num1a first_tri_initiation_date
				keep patid pregid flag_1st_trim_presc first_tri_initiation_date
				label var first_tri_initiation_date "Date of antidepressant initiation in 1st trim new users"
				
			}
			
			if _N>0 & `y'==2 {
			
				keep patid pregid pregstart_num any_o any_b presc_startdate_num1b cf_unexp_incid
				sort patid pregid presc_startdate_num1b
				br
				gen flag_2nd_trim_presc=1 if presc_startdate_num1b>=pregstart_num+91 & presc_startdate_num1b<pregstart_num+189
				keep if flag_2nd==1
				bysort pregid (presc_startdate_num1b): keep if _n==1
				codebook pregid
				rename presc_startdate_num1b second_tri_initiation_date
				keep patid pregid flag_2nd_trim_presc second_tri_initiation_date
				label var second_tri_initiation_date "Date of antidepressant initiation in 2nd trim new users"
				
			}
			
			if _N>0 & `y'==3 {
			
				keep patid pregid pregstart_num any_o any_c presc_startdate_num1c cf_unexp_incid pregend_num
				sort patid pregid presc_startdate_num1c
				br
				gen flag_3rd_trim_presc=1 if presc_startdate_num1c>=pregstart_num+189 & presc_startdate_num1c<pregend_num
				keep if flag_3rd==1
				bysort pregid (presc_startdate_num1c): keep if _n==1
				codebook pregid
				rename presc_startdate_num1c third_tri_initiation_date
				keep patid pregid flag_3rd_trim_presc third_tri_initiation_date
				label var third_tri_initiation_date "Date of antidepressant initiation in 3rd trim new users"
				
			}
			
			else if _N==0 {
				
				keep patid pregid
				
			}
			
			save "$Tempdatadir\dates_t`y'_new_users_`x'.dta", replace

		}
	}
		
	use "$Tempdatadir\dates_t1_new_users_1.dta"
	
	forvalues x=2/`max' {
		
		append using "$Tempdatadir\dates_t1_new_users_`x'.dta"
		
	}
	
	forvalues x=1/`max' {
		
		append using "$Tempdatadir\dates_t2_new_users_`x'.dta"
		append using "$Tempdatadir\dates_t3_new_users_`x'.dta"
		
	}
	
	duplicates drop patid pregid, force

	merge 1:1 patid pregid using "$Tempdatadir\pre_dates_new_users.dta", nogen // 2,772 with dates
	
	* if first trimester initiation date is pregnancy start date, add one day to intiation dates
	replace first_tri_initiation_date=first_tri_initiation_date+1 if pregstart_num==first_tri_initiation_date
	
	gen first_preg_initiation = first_tri_initiation_date if first_tri_initiation_date!=.
	replace first_preg_initiation = second_tri_initiation_date if second_tri_initiation_date!=.
	replace first_preg_initiation = third_tri_initiation_date if third_tri_initiation_date!=.
	
	* New-users after 37 weeks for the preterm delivery logistic regression
	
	gen date_37wk = pregstart_num + (37*7)
	gen flag_37wk_initiators = 1 if third_tri_initiation_date>date_37wk & third_tri_initiation_date!=.
	tab flag_37wk_initiators // should be considered unexposed in the preterm model
	
	* Exposures for the stratified preterm analysis - depending on the model, these flagged will be changed to unexposed, as they're unexposed until the end of the risk window for the outcome to occur
	
	gen date_32wk = (32*7)
	gen flag_32wk_initiators = 1 if third_tri_initiation_date>date_32wk & third_tri_initiation_date!=.
	tab flag_32wk_initiators
	
	gen date_28wk = (28*7)
	gen flag_28wk_initiators = 1 if third_tri_initiation_date>date_28wk & third_tri_initiation_date!=.
	tab flag_28wk_initiators
	
********************************************************************************

* Truncation

	summ pregstart_num, det
	
	gen trunc_flag =.
	replace trunc_flag = 1 if pregstart_num+(42*7)>21549 // latest start date in my data
	
	br if trunc_flag==1
	
	tab birth_yr
	tab birth_yr if trunc_flag==1 // accounts for all (minus 2) births in 2019 and 270 in 2018
	
********************************************************************************/

* Cox proportional hazards models

	* General start of follow up variable 22 weeks (start of risk period for most of the outcomes)
	
	gen start_fup = 153

	* Follow-up for Cox models - stillbirth
	
	gen end_fup_sb=gestdays

	* Follow-up for Cox models - neonatal death
	
	gen age_28days=gestdays+28
	gen end_fup_neonatal=age_28days

	* Follow-up for Cox models - preterm delivery
	
	gen gestage_37wks=(7*37)
	gen end_fup_preterm=min(gestdays, gestage_37wks)
	
		* Follow-up for Cox models - moderate-to-late preterm delivery
		
		gen gestage_32wks=(7*32)
		gen start_fup_modpreterm=gestage_32wks
		
		gen end_fup_modpreterm=min(gestdays, gestage_37wks)
		
		* Follow-up for Cox models - very preterm delivery
		
		gen gestage_28wks=(7*28)
		gen start_fup_verypreterm=gestage_28wks
		
		gen end_fup_verypreterm=min(gestdays, gestage_32wks)
		
		* Follow-up for Cox models - extremely preterm delivery
		
		gen end_fup_expreterm=min(gestdays, gestage_28wks)
	
	* Follow-up for Cox models - postterm delivery
	
	gen gestage_36plus6wks=(7*37)-1
	gen start_fup_postterm=gestage_36plus6wks
	
	gen end_fup_postterm=gestdays
	
	drop gestage*

* Time varying exposure switching on and off

	merge 1:1 patid pregid using "$Datadir\time_varying_exposure.dta", keep(1 3) nogen
	sort patid pregid
	
	br if cycle_1_start !=. & any_preg==0 // all have cycle starts
	
********************************************************************************
	
	rename pregstart_num pregstart
	rename pregend_num pregend
	
	*replace start_fup = 154
	
	* Trimester-specific exposures
	
	* Data management getting everything on the gestational age axis
	gen t1=.
	gen t2=.
	gen t3=.
	
	gen pregstart_num = 0
	gen pregend_num = gestdays
	
	* Data check
	count if cycle_1_start==pregend_num-1 // n=18
	count if cycle_1_start==pregend_num-2 // n=19
	
	forvalues x=1/3 {
		
		replace cycle_`x'_end = pregend_num if cycle_`x'_end>=pregend_num & cycle_`x'_end!=.
		
	}
	
	order patid pregid pregstart secondtrim_num thirdtrim_num pregend cycle* t1 t2 t3
	gen secondtrim = 91
	gen thirdtrim = 189
	
	forvalues x=1/3 {
		
		
		replace t1 = 1 if (cycle_`x'_end>=pregstart_num & cycle_`x'_end<secondtrim & cycle_`x'_end!=.) | (cycle_`x'_start>=pregstart_num & cycle_`x'_start<secondtrim & cycle_`x'_start!=.)  & secondtrim!=.
		
		
		replace t2 = 1 if (cycle_`x'_end>=secondtrim & cycle_`x'_end<thirdtrim & cycle_`x'_end!=.) | (cycle_`x'_start>=secondtrim & cycle_`x'_start<thirdtrim & cycle_`x'_start!=.) & secondtrim!=. & thirdtrim!=.
		replace t2 = 1 if (cycle_`x'_start>=pregstart_num & cycle_`x'_start<secondtrim & cycle_`x'_start!=.) & (cycle_`x'_end>=thirdtrim & cycle_`x'_end<=pregend_num & cycle_`x'_end!=.)  & secondtrim!=. & thirdtrim!=.
		
		
		replace t3 = 1 if (cycle_`x'_end>=thirdtrim & cycle_`x'_end<=pregend_num & cycle_`x'_end!=.) | (cycle_`x'_start>=thirdtrim & cycle_`x'_start<=pregend_num & cycle_`x'_start!=.) & thirdtrim!=.
		
		replace t`x' = 0 if t`x'==. 
		
	}

	* Exposure status at 22 weeks'
	
	gen any_22wk =.
	
	forvalues x=1/3 {
	
		replace any_22wk = 1 if cycle_`x'_start<start_fup & cycle_`x'_end<=start_fup 
	
	}
	
	forvalues x=1/3 {
	
		replace any_22wk = 2 if cycle_`x'_start<=start_fup & cycle_`x'_end>start_fup & cycle_`x'_start!=. & cycle_`x'_end!=.
		replace any_22wk = 2 if cycle_`x'_start>start_fup & cycle_`x'_end>start_fup & cycle_`x'_start!=. & cycle_`x'_end!=.
	
	}
	
	gen diff1 = cycle_2_start - cycle_1_end
	gen diff2 = cycle_3_start - cycle_2_end
	
	replace any_22wk = 0 if any_22wk==.
	
	tab any_22wk, m
	
	br pregstart_num secondtrim thirdtrim pregend_num cycle* t1 t2 t3 any_22wk start_fup*
	
	* Cycles from 22 weeks for the Cox models
	
	gen cycle_1_start_cox=. 
	gen cycle_1_end_cox=. 
	gen cycle_2_start_cox=. 
	gen cycle_2_end_cox=. 
	gen cycle_3_start_cox=. 
	gen cycle_3_end_cox=.
	
	* For those with an overlapping prescription with 22 weeks, they are exposed from 22 weeks
	replace cycle_1_start_cox = start_fup if any_22wk==2
	replace cycle_1_end_cox = cycle_1_end if cycle_1_end>start_fup & cycle_1_end!=. & any_22wk==2
	replace cycle_1_end_cox = cycle_2_end if cycle_2_end>start_fup & cycle_1_end_cox==. & any_22wk==2
	
	forvalues x=2/3 {
		
		replace cycle_`x'_start_cox = cycle_`x'_start if any_22wk==2 & cycle_`x'_start!=. & cycle_`x'_start>cycle_1_end_cox
		replace cycle_`x'_end_cox = cycle_`x'_end if cycle_`x'_start_cox!=. & any_22wk==2
		
	}
	
	* For those who discontinued at some point prior to 22 weeks
	replace cycle_1_start_cox = cycle_1_start if cycle_1_start>=start_fup & any_22wk==1 // n=0
	
	replace cycle_1_start_cox = cycle_2_start if cycle_2_start>=start_fup & any_22wk==1
	replace cycle_1_end_cox = cycle_2_end if cycle_1_start_cox!=. & any_22wk==1
	
	replace cycle_1_start_cox = cycle_3_start if cycle_3_start>=start_fup & cycle_1_start_cox==. & any_22wk==1
	replace cycle_1_end_cox = cycle_3_end if cycle_1_start_cox!=. & cycle_1_end_cox==. & any_22wk==1
	
	replace cycle_2_start_cox = cycle_3_start if cycle_3_start>cycle_2_end & cycle_3_start!=. & cycle_1_start_cox!=cycle_3_start & any_22wk==1
	replace cycle_2_end_cox = cycle_3_end if cycle_2_start_cox!=. & any_22wk==1
	
	* For those who were unexposed prior to 22 weeks
	replace cycle_1_start_cox = cycle_1_start if cycle_1_start>=start_fup & any_22wk==0
	replace cycle_1_end_cox = cycle_1_end if cycle_1_start_cox!=. & any_22wk==0
	
	replace cycle_2_start_cox = cycle_2_start if cycle_2_start>=cycle_1_end & cycle_2_start!=. & any_22wk==0 // n=0
	
	* Exposure status at 37 weeks'
	
	gen any_37wk =.
	
	forvalues x=1/3 {
	
		replace any_37wk = 1 if cycle_`x'_start<start_fup_postterm & cycle_`x'_end<=start_fup_postterm 
	
	}
	
	forvalues x=1/3 {
	
		replace any_37wk = 2 if cycle_`x'_start<start_fup_postterm & cycle_`x'_end>start_fup_postterm & cycle_`x'_start!=. & cycle_`x'_end!=.
	
	}
	
	replace any_37wk = 0 if any_37wk==.
	
	* Cycles from 37 weeks for the Cox models 
	
	gen cycle_1_start_postcox =.
	gen cycle_1_end_postcox =.
	
	replace cycle_1_start_postcox = cycle_1_start if cycle_1_start>start_fup_postterm & cycle_1_start!=.
	replace cycle_1_end_postcox = cycle_1_end if cycle_1_start_postcox!=.
	
	* Binary exposure for Cox
	
	rename any_22wk any_22wk_cat
	rename any_37wk any_37wk_cat
	
	* New-users after 37 weeks for the preterm delivery logistic regression
	
	gen flag_37wk_initiators = 1 if cycle_1_start>start_fup_postterm & cycle_1_start!=.
	tab flag_37wk_initiators // should be considered unexposed in the preterm model
	
	* Exposures for the stratified preterm analysis - depending on the model, these flagged will be changed to unexposed, as they're unexposed until the end of the risk window for the outcome to occur
	
	gen date_32wk = (32*7)
	gen flag_32wk_initiators = 1 if cycle_1_start>date_32wk & cycle_1_start!=.
	tab flag_32wk_initiators
	
	gen date_28wk = (28*7)
	gen flag_28wk_initiators = 1 if cycle_1_start>date_28wk & cycle_1_start!=.
	tab flag_28wk_initiators
	
********************************************************************************

* Truncation

	gen trunc_flag =.
	replace trunc_flag = 1 if pregstart+(42*7)>21549 // latest start date in my data
	
	br if trunc_flag==1
	
	tab birth_yr if trunc_flag==1 // accounts for about 50% of the births in the year 2020
	
********************************************************************************

* Retain required variables only

	keep patid pregid end* pregstart_num pregend_num birth_yr outcome ///
	lcd_num tod_num *fup* flag* trunc_flag ///
	stillborn term_cat *preterm* postterm* birth_weight *bweight* sga* lga* aga* gestdays gest_age_wks gestat unfeas_gestdays child_male /// /*outcomes*/
	any* cf* *_use t1 t2 t3 trim_cat drug_preg flag_*_initiators cycle* *fup* /// flag* preg_initiation_date /// /*primary exposure*/
	highest_dose_* class_* /// secondary exposure
	mother_age_cat matage mother_ethn AreaOfResidence imd_practice birth_yr_cat ///  /*demogs*/
	depression* anxiety* other_indic* ed mood dn migraine pain incont headache ///  /*indications*/
	smoke_preg mother_bmi_cat alcstatus  illicitdrug_preg ///   /*behaviours*/
	CPRD_consultation_events_cat diab hypbp hes_apc_e baby_hes_apc_e ///   /*h/c use*/
	asm_12mo ap_12mo folate_before ///  /*drug use*/
	hes_apc_e multiple_ev pregnum_new hist_loss grav_hist_sa grav_hist_sb parity spont* induc* weit_pct
	
	save "$Datadir\sens_whole_preg_reg.dta", replace
	
	keep if hes_apc_e==1 | baby_hes_apc_e==1

	save "$Datadir\primary_analysis_dataset.dta", replace

********************************************************************************

* Stop logging, translate .smcl into .pdf and erase .smcl

	log close primary_analysis_dataset
	
	translate "$Logdir\1_cleaning\3_primary analysis dataset.smcl" "$Logdir\1_cleaning\3_primary analysis dataset.pdf", replace
	
	erase "$Logdir\1_cleaning\3_primary analysis dataset.smcl"
	
********************************************************************************
