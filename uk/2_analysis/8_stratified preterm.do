/*******************************************************************************

	Generate table showing antidepressant use during pregnancy and preterm delivery stratified by: moderate-to-late (32-36+6), very (28-31+6), and extreme (<28 weeks gestation)

	Author: Flo Martin

	Date: 02/07/2024

	Table generated by this script
	
		- strat preterm.txt supplementary analysis logisitic regression models for each type of preterm delivery: moderate-to-late (32-37 weeks gestation at delivery), very (28-32 weeks), and extreme (<28 weeks)
		
*******************************************************************************/

* Start logging

	log using "$Logdir\2_analysis\8_stratified preterm", name(stratified_preterm) replace
	
********************************************************************************	
* Set up the table elements

	tempname myhandle	
	file open `myhandle' using "$Tabledir\strat preterm.txt", write replace
	
	file write `myhandle' "Outcome" _tab "Total" _tab "Exposed n/N (%)" _tab "Unexposed n/N (%)" _tab "OR" _tab "aOR*" _n
	
* Run the analyses
	
	foreach outcome in modpreterm verypreterm expreterm { 
	    
		use "$Datadir\primary_analysis_dataset.dta", clear
	
		* Ensure complete case analysis
		gen cc = 1 if birth_yr_cat!=. & mother_age_cat!=. & mother_ethn!=. & imd_practice!=. & parity!=. & ap_12mo!=. & asm_12mo!=. & depression!=. & anxiety!=. & CPRD_consultation_events_cat!=. & smoke_preg!=.
		keep if cc==1
		
		* Recode incident users after 37 weeks' unexposed for the moderate preterm delivery models, 32 weeks for very models etc.
		local y="`outcome'"
		
		if "`y'"=="modpreterm" {
		    
			replace any_preg=0 if flag_37wk_initiators==1
			tab any_preg
			
		}
		
		else if "`y'"=="verypreterm" {
		    
			replace any_preg=0 if flag_32wk_initiators==1
			tab any_preg
			
		}
		
		else {
		    
			replace any_preg=0 if flag_28wk_initiators==1
			tab any_preg
			
		}
			
		* Counts for the table	
		count if `outcome'!=.
		local total=`r(N)'
			
		count if  `outcome'==1  & any_preg==1
		local n_exp=`r(N)'
						
		count if  `outcome'==1  & any_preg==0
		local n_unexp=`r(N)'
								
		count if `outcome'!=. & any_preg==1
		local total_exp=`r(N)'
		local percent_exp=(`n_exp'/`total_exp')*100
						
		count if `outcome'!=. & any_preg==0
		local total_unexp=`r(N)'
		local percent_unexp=(`n_unexp'/`total_unexp')*100
						
		file write `myhandle' "`outcome'" _tab %7.0fc (`total') _tab %7.0fc (`n_exp') ("/") %7.0fc (`total_exp') (" (") %4.2f (`percent_exp') (")") _tab %7.0fc (`n_unexp') ("/") %7.0fc (`total_unexp') (" (") %4.2f (`percent_unexp') (")")
								
		* Unadjusted	
			
		logistic `outcome' i.any_preg, vce(cluster patid) or
			
		local tot=`e(N)'
		lincom 1.any_preg, or 
		local minadjor=`r(estimate)'
		local minadjuci=`r(ub)'
		local minadjlci=`r(lb)'
						
		file write `myhandle' _tab %4.2f (`minadjor') (" (") %4.2f (`minadjlci') ("-") %4.2f (`minadjuci') (")") 
		
		* Adjusted for all covariates
		
		logistic `outcome' i.any_preg birth_yr i.mother_age_cat i.mother_ethn i.smoke_preg i.grav_hist_sb i.imd_practice i.parity i.ap_12mo i.asm_12mo CPRD_consultation_events_cat depression anxiety, vce(cluster patid) or
		
		local tot=`e(N)'
		lincom 1.any_preg, or 
		local minadjor=`r(estimate)'
		local minadjuci=`r(ub)'
		local minadjlci=`r(lb)'

		file write `myhandle' _tab %4.2f (`minadjor') (" (") %4.2f (`minadjlci') ("-") %4.2f (`minadjuci') (")") _n
						
	}
	
********************************************************************************

* Stop logging, translate .smcl into .pdf and erase .smcl

	log close stratified_preterm
	
	translate "$Logdir\2_analysis\8_stratified preterm.smcl" "$Logdir\2_analysis\8_stratified preterm.pdf", replace
	
	erase "$Logdir\2_analysis\8_stratified preterm.smcl"
	
********************************************************************************
