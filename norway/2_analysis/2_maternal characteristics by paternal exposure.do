/*******************************************************************************

	Generate characteristics table by paternal exposure 

	Author: Flo Martin

	Date: 	11/12/2023

	Table generated by this script
	
		- maternal characteristics pat exposure.txt maternal characteristics by paternal exposure
		
*******************************************************************************/

* Start logging

	log using "$Logdir\2_analysis\2_maternal characteristics by pat exposure", name(mat_charac_pat_exposure) replace
	
********************************************************************************	

	* Generic code to output one row of table
	cap prog drop generaterow

	program define generaterow
		
		syntax, variable(varname) condition(string) outcome(string)
	
		* Put the varname and condition to left so that alignment can be checked vs shell
		file write tablecontent ("`variable'") _tab ("`condition'") _tab
		
		cou
		local overalldenom=r(N)
		
		cou if `variable' `condition'
		local rowdenom = r(N)
		local colpct = 100*(r(N)/`overalldenom')
		file write tablecontent %11.0fc (`rowdenom') (" (") %4.1f (`colpct') (")") _tab

		cou if any_preg_pat==1 
		local coldenom = r(N)
		cou if any_preg_pat==1 & `variable' `condition'
		local pct = 100*(r(N)/`coldenom')
		file write tablecontent %11.0fc (r(N)) (" (") %4.1f (`pct') (")") _tab

		cou if any_preg_pat==0 
		local coldenom = r(N)
		cou if any_preg_pat==0 & `variable' `condition'
		local pct = 100*(r(N)/`coldenom')
		file write tablecontent %11.0fc (r(N)) (" (") %4.1f (`pct') (")") _n
	
	end


********************************************************************************

* Generic code to output one section (varible) within table (calls above)

	cap prog drop tabulatevariable
	prog define tabulatevariable
		
		syntax, variable(varname) start(real) end(real) [missing] outcome(string)

		foreach varlevel of numlist `start'/`end'{ 
		
			generaterow, variable(`variable') condition("==`varlevel'") outcome(any_preg_pat)
	
		}
	
		if "`missing'"!="" generaterow, variable(`variable') condition(">=.") outcome(any_preg_pat)

	end

********************************************************************************

* Set up output file

		use "$Deriveddir\paternal_analysis_dataset.dta", clear
		
********************************************************************************
* 2 - Prepare formats for data for output
********************************************************************************

		cap file close tablecontent
		file open tablecontent using "$Tabledir\maternal characteristics pat exposure.txt", write text replace

		file write tablecontent "Variable" _tab "Level" _tab "Total" _tab "Exposed to antidepressants during pregnancy" _tab "Unexposed to antidepressants during pregnancy" _n
		
		gen byte total=1
		tabulatevariable, variable(total) start(1) end(1) outcome(any_preg_pat)
		
		file write tablecontent "Maternal characteristics" _n
		
		file write tablecontent "Year of pregnancy" _n

		tabulatevariable, variable(birth_yr_cat) start(1) end(3) outcome(any_preg_pat) 
		
		file write tablecontent "Maternal age at start of pregnancy" _n

		tabulatevariable, variable(mother_age_cat) start(0) end(6) outcome(any_preg_pat) 
		
		file write tablecontent "Maternal educational attainment at the start of pregnancy" _n

		tabulatevariable, variable(mother_educ) start(0) end(4) outcome(any_preg_pat)
		
		/*file write tablecontent "Combined household disposable income pre-pregnancy (in quintiles)" _n

		tabulatevariable, variable(dispink5atbirth) start(1) end(5) missing outcome(any_preg_pat)*/
		
		file write tablecontent "Maternal country of birth" _n

		tabulatevariable, variable(mother_birth_country_nonnorge) start(1) end(1) missing outcome(any_preg_pat)
		
		file write tablecontent "Maternal body mass index (BMI)" _n

		tabulatevariable, variable(mother_bmi_cat) start(0) end(3) missing outcome(any_preg_pat)
		
		file write tablecontent "Maternal smoking status at the start pregnancy" _n

		tabulatevariable, variable(smoke_preg) start(1) end(1) missing outcome(any_preg_pat) 

		/*file write tablecontent "Maternal alcohol intake during or before pregnancy" _n

		tabulatevariable, variable(alcstatus) start(0) end(2) missing outcome(any_preg_pat)
		
		file write tablecontent "Addiction during pregnancy" _n
	
		tabulatevariable, variable(addicted_bm) start(0) end(1) outcome(any_preg_pat)*/
		
		file write tablecontent "Maternal history of stillbirth at the start of pregnancy" _n

		tabulatevariable, variable(prev_sb_bin) start(1) end(1) missing outcome(any_preg_pat)
		/*tabulatevariable, variable(grav_hist_sb_) start(1) end(1) outcome(any_preg_pat)
		tabulatevariable, variable(grav_hist_top_) start(1) end(1) outcome(any_preg_pat)
		tabulatevariable, variable(grav_hist_otherloss_) start(1) end(1) outcome(any_preg_pat)*/

		file write tablecontent "Maternal parity at the start of pregnancy" _n
		
		tabulatevariable, variable(parity) start(0) end(4) outcome(any_preg_pat)
		
		/*file write tablecontent "Number of consultations in the 12 months before pregnancy" _n

		tabulatevariable, variable(visits_1y_cat) start(0) end(2) outcome(any_preg_pat)
 
		file write tablecontent "Number of consultations during pregnancy" _n
		
		tabulatevariable, variable(CPRD_consult_events_preg_cat) start(0) end(3) outcome(any_preg_pat)*/

		file write tablecontent "Maternal mental health problems ever before the end of pregnancy" _n

		tabulatevariable, variable(mat_depression) start(1) end(1) outcome(any_preg_pat)
		tabulatevariable, variable(mat_anxiety) start(1) end(1) outcome(any_preg_pat)
		tabulatevariable, variable(mat_ed) start(1) end(1) outcome(any_preg_pat)
		/*tabulatevariable, variable(bipolar_bm_preg) start(1) end(1) outcome(any_preg_pat)
		tabulatevariable, variable(schizo) start(1) end(1) outcome(any_preg_pat)
		tabulatevariable, variable(other_psych_bm_preg) start(1) end(1) outcome(any_preg_pat)*/
		
		file write tablecontent "Other possible somatic indications for antidepressants ever before pregnancy" _n

		tabulatevariable, variable(mat_affective) start(1) end(1) outcome(any_preg_pat)
		tabulatevariable, variable(mat_dn) start(1) end(1) outcome(any_preg_pat)
		tabulatevariable, variable(mat_migraine) start(1) end(1) outcome(any_preg_pat)
		tabulatevariable, variable(mat_narco) start(1) end(1) outcome(any_preg_pat)
		tabulatevariable, variable(mat_pain) start(1) end(1) outcome(any_preg_pat)
		tabulatevariable, variable(mat_stress_incont) start(1) end(1) outcome(any_preg_pat)
		tabulatevariable, variable(mat_tt_headache) start(1) end(1) outcome(any_preg_pat)

		/*file write tablecontent "Maternal neurodevelopmental disorder ever" _n

		tabulatevariable, variable(asd_bm) start(1) end(1) outcome(any_preg_pat)
		tabulatevariable, variable(adhd_bm) start(1) end(1) outcome(any_preg_pat)
		tabulatevariable, variable(id_bm) start(1) end(1) outcome(any_preg_pat)*/

		file write tablecontent "Chronic illness ever before pregnancy" _n

		tabulatevariable, variable(chronic_asthma) start(1) end(1) outcome(any_preg_pat)
		tabulatevariable, variable(chronic_htn) start(1) end(1) outcome(any_preg_pat)
		tabulatevariable, variable(chronic_renal) start(1) end(1) outcome(any_preg_pat)
		tabulatevariable, variable(chronic_epilepsy) start(1) end(1) outcome(any_preg_pat)
		tabulatevariable, variable(chronic_diabetes) start(1) end(1) outcome(any_preg_pat)
		
		file write tablecontent "Other mental health-related prescriptions in 12 months before pregnancy" _n
		
		tabulatevariable, variable(ap_12mo) start(1) end(1) outcome(any_preg_pat)
		*tabulatevariable, variable(benzos_preg) start(1) end(1) outcome(any_preg_pat) 
		*tabulatevariable, variable(zdrugs_preg) start(1) end(1) outcome(any_preg_pat)
		tabulatevariable, variable(asm_12mo) start(1) end(1) outcome(any_preg_pat) 
		
		file write tablecontent "Primary care consultations in 12 months before pregnancy" _n
		
		tabulatevariable, variable(healthcare_util_12mo_cat) start(0) end(3) outcome(any_preg_pat)
		
		file write tablecontent "Other prescriptions before pregnancy" _n
		
		/*tabulatevariable, variable(teratogen_preg) start(1) end(1) outcome(any_preg_pat) 
		tabulatevariable, variable(multivit_preg) start(1) end(1) outcome(any_preg_pat)*/ 
		tabulatevariable, variable(folate_before) start(1) end(1) outcome(any_preg_pat)
		/*tabulatevariable, variable(folic_preg1) start(1) end(1) outcome(any_preg_pat)
		tabulatevariable, variable(antiemetic) start(1) end(1) outcome(any_preg_pat)*/
		
		file write tablecontent "Initiation of labour" _n
		
		tabulatevariable, variable(spont_labour) start(0) end(0) outcome(any_preg_pat)
		
		file write tablecontent "Paternal characteristics" _n
		
		file write tablecontent "Paternal educational attainment at the start of pregnancy" _n
		
		tabulatevariable, variable(father_educ) start(0) end(4) outcome(any_preg_pat)
		
		file write tablecontent "Paternal mental health problems ever before the end of pregnancy" _n
		
		tabulatevariable, variable(pat_depression) start(1) end(1) outcome(any_preg_pat)
		tabulatevariable, variable(pat_anxiety) start(1) end(1) outcome(any_preg_pat)
		
		file close tablecontent
		
********************************************************************************

* Stop logging, translate .smcl into .pdf and erase .smcl

	log close mat_charac_pat_exposure
	
	translate "$Logdir\2_analysis\2_maternal characteristics by pat exposure.smcl" "$Logdir\2_analysis\2_maternal characteristics by pat exposure.pdf", replace
	
	erase "$Logdir\2_analysis\2_maternal characteristics by pat exposure.smcl"
	
********************************************************************************
