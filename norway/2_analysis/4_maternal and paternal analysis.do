/*******************************************************************************

	Primary maternal and paternal negative control analyses 

	Author: Flo Martin

	Date: 26/09/2023

	Tables generated by this script
	
		- primary analysis.txt primary maternal analysis and paternal logisitic regression models for each outcome
		- family analyses data.txt maternal, paternal and sibling analyses unrounded for figure creation
		
*******************************************************************************/

* Start logging

	log using "$Logdir\2_analysis\4_primary analysis", name(primary_analysis) replace
	
********************************************************************************	

	tempname myhandle	
	file open `myhandle' using "$Tabledir\primary analysis.txt", write replace
	
	file write `myhandle' "Outcome" _tab "Total complete mums" _tab "Maternal exposed n/N (%)" _tab "Maternal unexposed n/N (%)" _tab "OR" _tab "aOR*" _tab "Total complete dads" _tab "Paternal exposed n/N (%)" _tab "Paternal unexposed n/N (%)" _tab "OR" _tab "aOR**" _n
	
	foreach outcome in stillborn neonatal_death preterm postterm sga_pct lga_pct apgar5_bin { 
	    
		use "$Deriveddir\maternal_analysis_dataset.dta", clear
	
		* Ensure complete case analysis
		gen cc = 1 if birth_yr_cat!=. & mother_age_cat!=. & mother_educ!=. & mother_birth_country_nonnorge!=. & prev_sb_bin!=. & parity!=. & ap_12mo!=. & asm_12mo!=. & healthcare_util_12mo_cat!=. & depression!=. & anxiety!=. 
		keep if cc==1
		
		* Recode incident users after 37 weeks' unexposed for the preterm delivery modesls
		local y="`outcome'"
		if "`y'"=="preterm" {
		    
			replace any_preg=0 if flag_37wk_initiators==1
			tab any_preg
			
		}
		
		else {
		    
			tab any_preg
			
		}
			
		* Counts for the table	
		count if `outcome'!=.
		local total=`r(N)'
			
		count if  `outcome'==1  & any_preg==1
		local n_exp=`r(N)'
						
		count if  `outcome'==1  & any_preg==0
		local n_unexp=`r(N)'
								
		count if `outcome'!=. & any_preg==1
		local total_exp=`r(N)'
		local percent_exp=(`n_exp'/`total_exp')*100
						
		count if `outcome'!=. & any_preg==0
		local total_unexp=`r(N)'
		local percent_unexp=(`n_unexp'/`total_unexp')*100
						
		file write `myhandle' "`outcome'" _tab %7.0fc (`total') _tab %7.0fc (`n_exp') ("/") %7.0fc (`total_exp') (" (") %4.2f (`percent_exp') (")") _tab %7.0fc (`n_unexp') ("/") %7.0fc (`total_unexp') (" (") %4.2f (`percent_unexp') (")")
								
		* Unadjusted	
			
		logistic `outcome' i.any_preg, vce(cluster mother_id) or
			
		local tot=`e(N)'
		lincom 1.any_preg, or 
		local minadjor=`r(estimate)'
		local minadjuci=`r(ub)'
		local minadjlci=`r(lb)'
						
		file write `myhandle' _tab %4.2f (`minadjor') (" (") %4.2f (`minadjlci') ("-") %4.2f (`minadjuci') (")") 
		
		* Adjusted for all covariates
		
		logistic `outcome' i.any_preg birth_yr i.mother_age_cat i.mother_educ i.mother_birth_country_nonnorge i.prev_sb_bin i.parity i.ap_12mo i.asm_12mo healthcare_util_12mo depression anxiety, vce(cluster mother_id) or
		
		local tot=`e(N)'
		lincom 1.any_preg, or 
		local minadjor=`r(estimate)'
		local minadjuci=`r(ub)'
		local minadjlci=`r(lb)'

		file write `myhandle' _tab %4.2f (`minadjor') (" (") %4.2f (`minadjlci') ("-") %4.2f (`minadjuci') (")")
		
		use "$Deriveddir\paternal_analysis_dataset.dta", clear
	
		* Ensure complete case analysis
		gen cc = 1 if birth_yr_cat!=. & mother_age_cat!=. & mother_educ!=. & mother_birth_country_nonnorge!=. & prev_sb_bin!=. & parity!=. & ap_12mo!=. & asm_12mo!=. & healthcare_util_12mo_cat!=. & mat_depression!=. & mat_anxiety!=. & healthcare_util_12mo_cat!=. & pat_depression!=. & pat_anxiety!=. & father_educ!=.
		keep if cc==1
		
		* Recode incident users after 37 weeks' unexposed for the preterm delivery modesls
		local y="`outcome'"
		if "`y'"=="preterm" {
		    
			replace any_preg_pat=0 if flag_37wk_initiators==1
			tab any_preg_pat
			
		}
		
		else {
		    
			tab any_preg_pat
			
		}
		
		* Counts for the table
		count if `outcome'!=.
		local total=`r(N)'
			
		count if  `outcome'==1  & any_preg_pat==1
		local n_exp=`r(N)'
						
		count if  `outcome'==1  & any_preg_pat==0
		local n_unexp=`r(N)'
								
		count if `outcome'!=. & any_preg_pat==1
		local total_exp=`r(N)'
		local percent_exp=(`n_exp'/`total_exp')*100
						
		count if `outcome'!=. & any_preg_pat==0
		local total_unexp=`r(N)'
		local percent_unexp=(`n_unexp'/`total_unexp')*100
						
		file write `myhandle' _tab %7.0fc (`total') _tab %7.0fc (`n_exp') ("/") %7.0fc (`total_exp') (" (") %4.2f (`percent_exp') (")") _tab %7.0fc (`n_unexp') ("/") %7.0fc (`total_unexp') (" (") %4.2f (`percent_unexp') (")")
		
		* Unadjusted - paternal model
		
		logistic `outcome' i.any_preg_pat, or vce(cluster father_id)
		
		lincom 1.any_preg_pat, or
		local minadjor=`r(estimate)'
		local minadjuci=`r(ub)'
		local minadjlci=`r(lb)'

		file write `myhandle' _tab %4.2f (`minadjor') (" (") %4.2f (`minadjlci') ("-") %4.2f (`minadjuci') (")")
		
		* Adjusted for all covariates - paternal model
		
		logistic `outcome' i.any_preg_pat birth_yr i.mother_age_cat i.mother_educ i.mother_birth_country_nonnorge i.prev_sb_bin i.parity i.ap_12mo i.asm_12mo healthcare_util_12mo mat_depression mat_anxiety father_educ pat_depression pat_anxiety, or vce(cluster father_id)
		
		local tot=`e(N)'
		lincom 1.any_preg_pat, or
		local minadjor=`r(estimate)'
		local minadjuci=`r(ub)'
		local minadjlci=`r(lb)'

		file write `myhandle' _tab %4.2f (`minadjor') (" (") %4.2f (`minadjlci') ("-") %4.2f (`minadjuci') (")") _n
						
	}
	
	file close `myhandle'
	
********************************************************************************	

* FIGURE DATASET

	tempname myhandle	
	file open `myhandle' using "$Tabledir\family analyses data.txt", write replace
	
	file write `myhandle' "outcome" _tab "model" _tab "country" _tab "total" _tab "total_exp" _tab "total_unexp" _tab "or" _tab "lci" _tab "uci" _n
	
	foreach outcome in stillborn neonatal_death preterm postterm sga_pct lga_pct apgar5_bin { 
	    
		* Maternal primary analysis
		
		use "$Deriveddir\maternal_analysis_dataset.dta", clear
		
		drop if birth_yr<2009
	
		gen cc = 1 if birth_yr_cat!=. & mother_age_cat!=. & mother_educ!=. & mother_birth_country_nonnorge!=. & prev_sb_bin!=. & parity!=. & ap_12mo!=. & asm_12mo!=. & healthcare_util_12mo_cat!=. & depression!=. & anxiety!=. 
		keep if cc==1
		
		file write `myhandle' "`outcome'" _tab "maternal" _tab "no"
		
		* Adjusted for all covariates
		
		* Recode incident users after 37 weeks' unexposed for the preterm delivery modesls
		local y="`outcome'"
		if "`y'"=="preterm" {
		    
			replace any_preg=0 if flag_37wk_initiators==1
			tab any_preg
			
		}
		
		else {
		    
			tab any_preg
			
		}
		
		* Counts for the table	
		count if `outcome'==1 & any_preg==1
		local exp_n=`r(N)' 
			
		count if any_preg==1 & `outcome'!=.
		local exp=`r(N)'
			
		count if `outcome'==1 & any_preg==0
		local unexp_n=`r(N)' 
			
		count if any_preg==0 & `outcome'!=.
		local unexp=`r(N)'
		
		logistic `outcome' i.any_preg birth_yr i.mother_age_cat i.mother_educ i.mother_birth_country_nonnorge i.prev_sb_bin i.parity i.ap_12mo i.asm_12mo healthcare_util_12mo depression anxiety, vce(cluster mother_id) or
		
		lincom 1.any_preg, or 
		local tot=`e(N)'
		local or=`r(estimate)'
		local uci=`r(ub)'
		local lci=`r(lb)'
		
		file write `myhandle' _tab (`tot') _tab %6.0fc (`exp_n') ("/") %6.0fc (`exp') _tab %6.0fc (`unexp_n') ("/") %7.0fc (`unexp') _tab (`or') _tab (`lci') _tab (`uci') _n
		
		* Paternal negative control analysis
		
		use "$Deriveddir\paternal_analysis_dataset.dta", clear
		
		drop if birth_yr<2009
	
		gen cc = 1 if birth_yr_cat!=. & mother_age_cat!=. & mother_educ!=. & mother_birth_country_nonnorge!=. & prev_sb_bin!=. & parity!=. & ap_12mo!=. & asm_12mo!=. & healthcare_util_12mo_cat!=. & mat_depression!=. & mat_anxiety!=. & pat_depression!=. & pat_anxiety!=. & father_educ!=.
		keep if cc==1
		
		file write `myhandle' "`outcome'" _tab "paternal" _tab "no"
		
		* Adjusted for all covariates
		
		* Recode incident users after 37 weeks' unexposed for the preterm delivery modesls
		local y="`outcome'"
		if "`y'"=="preterm" {
		    
			replace any_preg_pat=0 if flag_37wk_initiators==1
			tab any_preg_pat
			
		}
		
		else {
		    
			tab any_preg_pat
			
		}
		
		* Counts for the table	
		count if `outcome'==1 & any_preg_pat==1
		local exp_n=`r(N)' 
			
		count if any_preg_pat==1 & `outcome'!=.
		local exp=`r(N)'
			
		count if `outcome'==1 & any_preg_pat==0
		local unexp_n=`r(N)' 
			
		count if any_preg_pat==0 & `outcome'!=.
		local unexp=`r(N)'
		
		logistic `outcome' i.any_preg_pat birth_yr i.mother_age_cat i.mother_educ i.mother_birth_country_nonnorge i.prev_sb_bin i.parity i.ap_12mo i.asm_12mo healthcare_util_12mo mat_depression mat_anxiety pat_depression pat_anxiety father_educ, or vce(cluster father_id)
		
		lincom 1.any_preg_pat, or 
		local tot=`e(N)'
		local or=`r(estimate)'
		local uci=`r(ub)'
		local lci=`r(lb)'
		
		file write `myhandle' _tab (`tot') _tab %6.0fc (`exp_n') ("/") %6.0fc (`exp') _tab %6.0fc (`unexp_n') ("/") %7.0fc (`unexp') _tab (`or') _tab (`lci') _tab (`uci') _n
		
		* Sibling analysis
		
		use "$Deriveddir\sibling_analysis.dta", clear
		
		gen cc = 1 if birth_yr_cat!=. & mother_age_cat!=. & mother_educ!=. & mother_birth_country_nonnorge!=. & prev_sb_bin!=. & parity!=. & ap_12mo!=. & asm_12mo!=. & depression!=. & anxiety!=. & healthcare_util_12mo_cat!=. 
		keep if cc==1
		
		file write `myhandle' "`outcome'" _tab "sibling" _tab "no"
		
		* Use the preterm exposure variable for the preterm models (no new users after 37 weeks' in the exposed group)
		local y="`outcome'"
		if "`y'"=="preterm" {
			
			capture noisily clogit `outcome' i.any_preg_preterm birth_yr_model i.mother_age_cat i.mother_educ i.parity ap_12mo_model asm_12mo_model healthcare_util_12mo_model depression_model anxiety_model, group(mother_id) or
			
			tabcount any_preg_preterm `outcome' if e(sample), v1(0/1) v2(0/1) matrix(exposed)
			local total=`e(N)'
			local unexp_lb=exposed[1,1]
			local unexp_sb=exposed[1,2]
			local unexp_n=`unexp_lb'+`unexp_sb'
			local exp_lb=exposed[2,1]
			local exp_sb=exposed[2,2]
			local exp_n=`exp_lb'+`exp_sb'
			
			local tot=`e(N)'
			lincom 1.any_preg_preterm, or 
			local or=`r(estimate)'
			local uci=`r(ub)'
			local lci=`r(lb)'
			
			file write `myhandle' _tab (`tot') _tab %6.0fc (`exp_sb') ("/") %6.0fc (`exp_n') _tab %6.0fc (`unexp_sb') ("/") %7.0fc (`unexp_n') _tab (`or') _tab (`lci') _tab (`uci') _n
			
		}
		
		else { 
			
			capture noisily clogit `outcome' i.any_preg birth_yr_model i.mother_age_cat i.mother_educ i.parity ap_12mo_model asm_12mo_model healthcare_util_12mo_model depression_model anxiety_model, group(mother_id) or
			
			tabcount any_preg_preterm `outcome' if e(sample), v1(0/1) v2(0/1) matrix(exposed)
			local total=`e(N)'
			local unexp_lb=exposed[1,1]
			local unexp_sb=exposed[1,2]
			local unexp_n=`unexp_lb'+`unexp_sb'
			local exp_lb=exposed[2,1]
			local exp_sb=exposed[2,2]
			local exp_n=`exp_lb'+`exp_sb'
			
			local tot=`e(N)'
			lincom 1.any_preg, or 
			local or=`r(estimate)'
			local uci=`r(ub)'
			local lci=`r(lb)'
		
			file write `myhandle' _tab (`tot') _tab %6.0fc (`exp_sb') ("/") %6.0fc (`exp_n') _tab %6.0fc (`unexp_sb') ("/") %7.0fc (`unexp_n') _tab (`or') _tab (`lci') _tab (`uci') _n 
			
		} 
		
	}
 
********************************************************************************

* Stop logging, translate .smcl into .pdf and erase .smcl

	log close primary_analysis
	
	translate "$Logdir\2_analysis\4_primary analysis.smcl" "$Logdir\2_analysis\4_primary analysis.pdf", replace
	
	erase "$Logdir\2_analysis\4_primary analysis.smcl"
	
********************************************************************************
