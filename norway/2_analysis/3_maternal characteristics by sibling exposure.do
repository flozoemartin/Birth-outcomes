/*******************************************************************************

	Generate characteristics table by discordant siblings

	Author: Flo Martin

	Date: 	22/09/2023

	Table generated by this script
	
		- maternal characteristics by sibs.txt maternal characteristics by exposed and unexposed siblings
		
*******************************************************************************/

* Start logging

	log using "$Logdir\2_analysis\3_maternal characteristics by sibs", name(mat_charac_by_sibs) replace
	
********************************************************************************	

	* Generic code to output one row of table
	cap prog drop generaterow

	program define generaterow
		
		syntax, variable(varname) condition(string) outcome(string)
	
		* Put the varname and condition to left so that alignment can be checked vs shell
		file write tablecontent ("`variable'") _tab ("`condition'") _tab
		
		cou if firstsibexp_any==1 | secondsibexp_any ==1
		local overalldenom=r(N)
		
		cou if `variable' `condition'
		local rowdenom = r(N)
		local colpct = 100*(r(N)/`overalldenom')
		file write tablecontent %11.0fc (`rowdenom') (" (") %4.1f (`colpct') (")") _tab

		cou if any_preg==1 & (firstsibexp_any==1 | secondsibexp_any ==1)
		local coldenom = r(N)
		cou if any_preg==1 & `variable' `condition'
		local pct = 100*(r(N)/`coldenom')
		file write tablecontent %11.0fc (r(N)) (" (") %4.1f (`pct') (")") _tab

		cou if any_preg==0 & (firstsibexp_any==1 | secondsibexp_any ==1)
		local coldenom = r(N)
		cou if any_preg==0 & `variable' `condition'
		local pct = 100*(r(N)/`coldenom')
		file write tablecontent %11.0fc (r(N)) (" (") %4.1f (`pct') (")") _n
	
	end


********************************************************************************

* Generic code to output one section (varible) within table (calls above)

	cap prog drop tabulatevariable
	prog define tabulatevariable
		
		syntax, variable(varname) start(real) end(real) [missing] outcome(string)

		foreach varlevel of numlist `start'/`end'{ 
		
			generaterow, variable(`variable') condition("==`varlevel'") outcome(any_preg)
	
		}
	
		if "`missing'"!="" generaterow, variable(`variable') condition(">=.") outcome(any_preg)

	end

********************************************************************************

* Set up output file

		use "$Deriveddir\sibling_analysis.dta", clear
		
		keep if (firstsibexp_any==1 | secondsibexp_any ==1)
		
********************************************************************************
* 2 - Prepare formats for data for output
********************************************************************************

		cap file close tablecontent
		file open tablecontent using "$Tabledir\maternal characteristics by sibs.txt", write text replace

		file write tablecontent "Variable" _tab "Level" _tab "Total" _tab "Exposed to antidepressants during pregnancy" _tab "Unexposed to antidepressants during pregnancy" _n
		
		gen byte total=1
		tabulatevariable, variable(total) start(1) end(1) outcome(any_preg)
		
		file write tablecontent "Year of pregnancy" _n

		tabulatevariable, variable(birth_yr_cat) start(1) end(3) outcome(any_preg) 
		
		file write tablecontent "Maternal age at start of pregnancy" _n

		tabulatevariable, variable(mother_age_cat) start(0) end(6) outcome(any_preg) 
		
		file write tablecontent "Maternal educational attainment at the start of pregnancy" _n

		tabulatevariable, variable(mother_educ) start(0) end(4) outcome(any_preg)
		
		/*file write tablecontent "Combined household disposable income pre-pregnancy (in quintiles)" _n

		tabulatevariable, variable(dispink5atbirth) start(1) end(5) missing outcome(any_preg)*/
		
		file write tablecontent "Maternal country of birth" _n

		tabulatevariable, variable(mother_birth_country_nonnorge) start(1) end(1) missing outcome(any_preg)
		
		file write tablecontent "Maternal body mass index (BMI)" _n

		tabulatevariable, variable(mother_bmi_cat) start(0) end(3) missing outcome(any_preg)
		
		file write tablecontent "Maternal smoking status at the start pregnancy" _n

		tabulatevariable, variable(smoke_preg) start(1) end(1) missing outcome(any_preg) 

		/*file write tablecontent "Maternal alcohol intake during or before pregnancy" _n

		tabulatevariable, variable(alcstatus) start(0) end(2) missing outcome(any_preg)
		
		file write tablecontent "Addiction during pregnancy" _n
	
		tabulatevariable, variable(addicted_bm) start(0) end(1) outcome(any_preg)*/
		
		file write tablecontent "Maternal history of stillbirth at the start of pregnancy" _n

		tabulatevariable, variable(prev_sb_bin) start(1) end(1) missing outcome(any_preg)
		/*tabulatevariable, variable(grav_hist_sb_) start(1) end(1) outcome(any_preg)
		tabulatevariable, variable(grav_hist_top_) start(1) end(1) outcome(any_preg)
		tabulatevariable, variable(grav_hist_otherloss_) start(1) end(1) outcome(any_preg)*/

		file write tablecontent "Maternal parity at the start of pregnancy" _n
		
		tabulatevariable, variable(parity) start(0) end(4) outcome(any_preg)
		
		/*file write tablecontent "Number of consultations in the 12 months before pregnancy" _n

		tabulatevariable, variable(visits_1y_cat) start(0) end(2) outcome(any_preg)
 
		file write tablecontent "Number of consultations during pregnancy" _n
		
		tabulatevariable, variable(CPRD_consult_events_preg_cat) start(0) end(3) outcome(any_preg)*/

		file write tablecontent "Maternal mental health problems ever before pregnancy" _n

		tabulatevariable, variable(depression) start(1) end(1) outcome(any_preg)
		tabulatevariable, variable(anxiety) start(1) end(1) outcome(any_preg)
		tabulatevariable, variable(ed) start(1) end(1) outcome(any_preg)
		/*tabulatevariable, variable(bipolar_bm_preg) start(1) end(1) outcome(any_preg)
		tabulatevariable, variable(schizo) start(1) end(1) outcome(any_preg)
		tabulatevariable, variable(other_psych_bm_preg) start(1) end(1) outcome(any_preg)*/
		
		file write tablecontent "Other possible somatic indications for antidepressants ever before pregnancy" _n

		tabulatevariable, variable(affective) start(1) end(1) outcome(any_preg)
		tabulatevariable, variable(dn) start(1) end(1) outcome(any_preg)
		tabulatevariable, variable(migraine) start(1) end(1) outcome(any_preg)
		tabulatevariable, variable(narco) start(1) end(1) outcome(any_preg)
		tabulatevariable, variable(pain) start(1) end(1) outcome(any_preg)
		tabulatevariable, variable(stress_incont) start(1) end(1) outcome(any_preg)
		tabulatevariable, variable(tt_headache) start(1) end(1) outcome(any_preg)

		/*file write tablecontent "Maternal neurodevelopmental disorder ever" _n

		tabulatevariable, variable(asd_bm) start(1) end(1) outcome(any_preg)
		tabulatevariable, variable(adhd_bm) start(1) end(1) outcome(any_preg)
		tabulatevariable, variable(id_bm) start(1) end(1) outcome(any_preg)*/

		file write tablecontent "Chronic illness ever before pregnancy" _n

		tabulatevariable, variable(chronic_asthma) start(1) end(1) outcome(any_preg)
		tabulatevariable, variable(chronic_htn) start(1) end(1) outcome(any_preg)
		tabulatevariable, variable(chronic_renal) start(1) end(1) outcome(any_preg)
		tabulatevariable, variable(chronic_epilepsy) start(1) end(1) outcome(any_preg)
		tabulatevariable, variable(chronic_diabetes) start(1) end(1) outcome(any_preg)
		
		file write tablecontent "Other mental health-related prescriptions in 12 months before pregnancy" _n
		
		tabulatevariable, variable(ap_12mo) start(1) end(1) outcome(any_preg)
		*tabulatevariable, variable(benzos_preg) start(1) end(1) outcome(any_preg) 
		*tabulatevariable, variable(zdrugs_preg) start(1) end(1) outcome(any_preg)
		tabulatevariable, variable(asm_12mo) start(1) end(1) outcome(any_preg) 
		
		file write tablecontent "Primary care consultations in 12 months before pregnancy" _n
		
		tabulatevariable, variable(healthcare_util_12mo_cat) start(0) end(3) outcome(any_preg)
		
		file write tablecontent "Other prescriptions before pregnancy" _n
		
		/*tabulatevariable, variable(teratogen_preg) start(1) end(1) outcome(any_preg) 
		tabulatevariable, variable(multivit_preg) start(1) end(1) outcome(any_preg)*/ 
		tabulatevariable, variable(folate_before) start(1) end(1) outcome(any_preg)
		/*tabulatevariable, variable(folic_preg1) start(1) end(1) outcome(any_preg)
		tabulatevariable, variable(antiemetic) start(1) end(1) outcome(any_preg)*/
		
		file write tablecontent "Initiation of labour" _n
		
		tabulatevariable, variable(spont_labour) start(0) end(0) outcome(any_preg)
		
		file close tablecontent
		
********************************************************************************

* Stop logging, translate .smcl into .pdf and erase .smcl

	log close mat_charac_by_sibs
	
	translate "$Logdir\2_analysis\3_maternal characteristics by sibs.smcl" "$Logdir\2_analysis\3_maternal characteristics by sibs.pdf", replace
	
	erase "$Logdir\2_analysis\3_maternal characteristics by sibs.smcl"
	
********************************************************************************
