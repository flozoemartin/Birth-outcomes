
/*******************************************************************************

	Generate data for the UK analyses for meta-analysis

	Author: Flo Martin

	Date: 27/11/2023

	Table generated by this script
	
		- drug_figure_no.pdf drug-specific analyses results in Norway
		
*******************************************************************************/

* Start logging

	log using "$Logdir\meta analysis\3a_family analyses fig data uk", name(family_analyses_fig_data_uk) replace
	
********************************************************************************	

* Set up the dataframe elements

	tempname myhandle	
	file open `myhandle' using "$Graphdir\data\family analyses data_uk.txt", write replace
	
	file write `myhandle' "outcome" _tab "model" _tab "country" _tab "total" _tab "total_exp" _tab "total_unexp" _tab "or" _tab "lci" _tab "uci" _n
	
	foreach outcome in stillborn preterm postterm sga_pct lga_pct { 
	    
		* Maternal primary analysis
		
		use "$Datadir\primary_analysis_dataset.dta", clear
	
		gen cc = 1 if birth_yr_cat!=. & mother_age_cat!=. & imd_practice!=. & parity!=. & ap_12mo!=. & asm_12mo!=. & depression!=. & anxiety!=. & CPRD_consultation_events_cat!=. & smoke_preg!=. & mother_ethn!=.
		keep if cc==1
		
		file write `myhandle' "`outcome'" _tab "maternal" _tab "uk"
		
		* Adjusted for all covariates
		
		* Recode incident users after 37 weeks' unexposed for the preterm delivery modesls
		local y="`outcome'"
		if "`y'"=="preterm" {
		    
			replace any_preg=0 if flag_37wk_initiators==1
			tab any_preg
			
		}
		
		else {
		    
			tab any_preg
			
		}
		
		* Counts for the table	
		count if `outcome'==1 & any_preg==1
		local exp_n=`r(N)' 
			
		count if any_preg==1 & `outcome'!=.
		local exp=`r(N)'
			
		count if `outcome'==1 & any_preg==0
		local unexp_n=`r(N)' 
			
		count if any_preg==0 & `outcome'!=.
		local unexp=`r(N)'
		
		logistic `outcome' i.any_preg birth_yr i.mother_age_cat i.mother_ethn i.smoke_preg i.grav_hist_sb i.imd_practice i.parity i.ap_12mo i.asm_12mo CPRD_consultation_events_cat depression anxiety, vce(cluster patid) or
		
		local tot=`e(N)'
		lincom 1.any_preg, or 
		local or=`r(estimate)'
		local uci=`r(ub)'
		local lci=`r(lb)'
		
		file write `myhandle' _tab (`tot') _tab %6.0fc (`exp_n') ("/") %6.0fc (`exp') _tab %6.0fc (`unexp_n') ("/") %7.0fc (`unexp') _tab (`or') _tab (`lci') _tab (`uci') _n
		
		* Sibling analysis
		
		use "$Deriveddir\sibling_analysis.dta", clear
		
		gen cc = 1 if birth_yr_cat!=. & mother_age_cat!=. & imd_practice!=. & parity!=. & ap_12mo!=. & asm_12mo!=. & depression!=. & anxiety!=. & healthcare_util_12mo_cat!=. & smoke_preg!=. & mother_ethn!=.
	keep if cc==1
		
		file write `myhandle' "`outcome'" _tab "sibling" _tab "uk"
		
		* Use the preterm exposure variable for the preterm models (no new users after 37 weeks' in the exposed group)
		local y="`outcome'"
		if "`y'"=="preterm" {
				
			capture noisily clogit `outcome' i.any_preg_preterm birth_yr_model i.smoke_preg i.mother_age_cat i.imd_practice i.parity ap_12mo_model asm_12mo_model healthcare_util_12mo_cat_model depression_model anxiety_model, group(patid) or
			
			tabcount any_preg_preterm `outcome' if e(sample), v1(0/1) v2(0/1) matrix(exposed)
			local total=`e(N)'
			local unexp_lb=exposed[1,1]
			local unexp_sb=exposed[1,2]
			local unexp_n=`unexp_lb'+`unexp_sb'
			local exp_lb=exposed[2,1]
			local exp_sb=exposed[2,2]
			local exp_n=`exp_lb'+`exp_sb'
			
			local tot=`e(N)'
			lincom 1.any_preg_preterm, or 
			local or=`r(estimate)'
			local uci=`r(ub)'
			local lci=`r(lb)'
			
			file write `myhandle' _tab (`tot') _tab %6.0fc (`exp_sb') ("/") %6.0fc (`exp_n') _tab %6.0fc (`unexp_sb') ("/") %7.0fc (`unexp_n') _tab (`or') _tab (`lci') _tab (`uci') _n
			
		}
		
		else { 
			
			capture noisily clogit `outcome' i.any_preg birth_yr_model i.mother_age_cat i.smoke_preg i.imd_practice i.mother_ethn i.parity ap_12mo_model asm_12mo_model healthcare_util_12mo_cat_model depression_model anxiety_model, group(patid) or
			
			tabcount any_preg `outcome' if e(sample), v1(0/1) v2(0/1) matrix(exposed)
			local total=`e(N)'
			local unexp_lb=exposed[1,1]
			local unexp_sb=exposed[1,2]
			local unexp_n=`unexp_lb'+`unexp_sb'
			local exp_lb=exposed[2,1]
			local exp_sb=exposed[2,2]
			local exp_n=`exp_lb'+`exp_sb'
			
			local tot=`e(N)'
			lincom 1.any_preg, or 
			local or=`r(estimate)'
			local uci=`r(ub)'
			local lci=`r(lb)'
		
			file write `myhandle' _tab (`tot') _tab %6.0fc (`exp_sb') ("/") %6.0fc (`exp_n') _tab %6.0fc (`unexp_sb') ("/") %7.0fc (`unexp_n') _tab (`or') _tab (`lci') _tab (`uci') _n
			
		}	
	}
	
	file close `myhandle'
	
* Get sibling analysis counts from the sibling analysis table (from tabcount)

	import delimited using "$Tabledir\sibling analysis.txt", varnames(1) clear
	
	keep outcome total *nn
	rename exposednn total_exp
	rename unexposednn total_unexp
	
	replace outcome="sga" if outcome=="sga_pct"
	replace outcome="lga" if outcome=="lga_pct"
	
	gen country="uk"
	gen model="sibling"
	
	replace total = subinstr(total, ",", "", .)
	gen total_num=real(total)
	drop total
	rename total_num total
	
	save "$Graphdir\data\uk_counts_sibs.dta", replace
	
* Convert to .dta

	import delimited using "$Graphdir\data\family analyses data_uk.txt", clear
	
	replace outcome="sga" if outcome=="sga_pct"
	replace outcome="lga" if outcome=="lga_pct"
	
	drop if model==""
	
	save "$Graphdir\data\family analyses data_uk.dta", replace
	
	erase "$Graphdir\data\family analyses data_uk.txt"
	
********************************************************************************

* Stop logging, translate .smcl into .pdf and erase .smcl

	log close family_analyses_fig_data_uk
	
	translate "$Logdir\meta analysis\3a_family analyses fig data uk.smcl" "$Logdir\meta analysis\3a_family analyses fig data uk.pdf", replace
	
	erase "$Logdir\meta analysis\3a_family analyses fig data uk.smcl"
	
********************************************************************************	
