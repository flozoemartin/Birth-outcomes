
/*******************************************************************************

	Combine the data and counts from the UK, Norway, and Sweden

	Author: Flo Martin

	Date: 27/11/2023

	Table generated by this script
	
		- $Graphdir\data\family analyses data_all3.dta drug-specific analyses results in Norway
		
*******************************************************************************/

* Start logging

	log using "$Logdir\meta analysis\3b_combining family analysis data", name(combining_family_analysis_data) replace
	
********************************************************************************	

* Norway
* Maternal model counts

	import delimited using "$NOTabledir\primary analysis.txt", varnames(1) clear
	
	keep outcome *mums maternal*
	
	rename totalcompletemums total
	egen total_exp = ends(maternalexposednn), punct("(") trim head
	egen total_unexp = ends(maternalunexposednn), punct("(") trim head
	
	gen country="no"
	gen model="maternal"
	
	replace outcome="sga" if outcome=="sga_pct"
	replace outcome="lga" if outcome=="lga_pct"
	
	keep outcome total* country model
	
	save "$Graphdir\data\no_counts_mat.dta", replace
	
* Paternal model counts
	
	import delimited using "$NOTabledir\primary analysis.txt", varnames(1) clear
	
	keep outcome *dads paternal*
	
	rename totalcompletedads total
	egen total_exp = ends(paternalexposednn), punct("(") trim head
	egen total_unexp = ends(paternalunexposednn), punct("(") trim head
	
	gen country="no"
	gen model="paternal"
	
	replace outcome="sga" if outcome=="sga_pct"
	replace outcome="lga" if outcome=="lga_pct"
	
	keep outcome total* country model
	
	save "$Graphdir\data\no_counts_pat.dta", replace
	
* Sibling model counts

	import delimited using "$NOTabledir\sibling analysis.txt", varnames(1) clear
	
	keep outcome total *nn
	rename exposednn total_exp
	rename unexposednn total_unexp
	
	replace outcome="sga" if outcome=="sga_pct"
	replace outcome="lga" if outcome=="lga_pct"
	
	gen country="no"
	gen model="sibling"
	
	save "$Graphdir\data\no_counts_sibs.dta", replace

********************************************************************************	
	
* Sweden
* Maternal model counts

	import delimited using "$SETabledir\data for figures\mat analyses data se.txt", varnames(1) clear
	
	replace outcome="sga" if outcome=="sga_pct"
	replace outcome="lga" if outcome=="lga_pct"
	
	replace total = subinstr(total, ",", "", .)
	gen total_num=real(total)
	drop total
	rename total_num total
	
	save "$Graphdir\data\se_counts_mat.dta", replace
	
* Paternal model counts
	
	import delimited using "$SETabledir\data for figures\pat analyses data se.txt", varnames(1) clear
	
	replace outcome="sga" if outcome=="sga_pct"
	replace outcome="lga" if outcome=="lga_pct"
	
	replace total = subinstr(total, ",", "", .)
	gen total_num=real(total)
	drop total
	rename total_num total
	
	save "$Graphdir\data\se_counts_pat.dta", replace
	
* Sibling model counts

	import delimited using "$SETabledir\data for figures\sib analyses data se.txt", varnames(1) clear
	
	replace outcome="sga" if outcome=="sga_pct"
	replace outcome="lga" if outcome=="lga_pct"
	
	replace total = subinstr(total, ",", "", .)
	gen total_num=real(total)
	drop total
	rename total_num total
	
	save "$Graphdir\data\se_counts_sibs.dta", replace

* Load in the Swedish text file and convert to .dta

	use "$Graphdir\data\se_counts_mat.dta", clear
	append using "$Graphdir\data\se_counts_pat.dta"
	append using "$Graphdir\data\se_counts_sibs.dta"
	
	save "$Graphdir\data\family analyses data_se.dta", replace
	
********************************************************************************

* Combining the summary level data from the three countries

	use "$Graphdir\data\family analyses data_no.dta", clear
	
	drop total*
	
	replace outcome="sga" if outcome=="sga_pct"
	replace outcome="lga" if outcome=="lga_pct"
	
	drop if model==""
	
	merge 1:1 outcome model using "$Graphdir\data\no_counts_mat.dta", update replace nogen
	merge 1:1 outcome model using "$Graphdir\data\no_counts_pat.dta", update replace nogen
	merge 1:1 outcome model using "$Graphdir\data\no_counts_sibs.dta", update replace nogen
	
	replace total = subinstr(total, ",", "", .)
	gen total_num=real(total)
	drop total
	rename total_num total
	
	append using "$Graphdir\data\family analyses data_uk.dta", force
	append using "$Graphdir\data\family analyses data_se.dta"
	
	replace total_exp = subinstr(total_exp, " ", "", .)
	replace total_unexp = subinstr(total_unexp, " ", "", .)
	
	replace total_exp = subinstr(total_exp, "/", " / ", .)
	replace total_unexp = subinstr(total_unexp, "/", " / ", .)
	
	replace outcome= "a_stillborn" if outcome=="stillborn"
	replace outcome= "b_neonatal_death" if outcome=="neonatal_death"
	replace outcome= "c_preterm" if outcome=="preterm"
	replace outcome= "d_postterm" if outcome=="postterm"
	replace outcome= "e_sga" if outcome=="sga"
	replace outcome= "f_lga" if outcome=="lga"
	replace outcome= "g_apar5_bin" if outcome=="apgar5_bin"
	
	gen seq=1 if country=="uk"
	replace seq=2 if country=="no"
	replace seq=3 if country=="se"
	
	sort outcome model seq
	
	save "$Graphdir\data\family analyses data_all3.dta", replace
	
********************************************************************************

* Stop logging, translate .smcl into .pdf and erase .smcl

	log close combining_family_analysis_data
	
	translate "$Logdir\meta analysis\3b_combining family analysis data.smcl" "$Logdir\meta analysis\3b_combining family analysis data.pdf", replace
	
	erase "$Logdir\meta analysis\3b_combining family analysis data.smcl"
	
********************************************************************************
