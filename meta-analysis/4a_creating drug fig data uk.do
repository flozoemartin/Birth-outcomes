
/*******************************************************************************

	Generate data for the drug-specific analysis in one country (UK)

	Author: Flo Martin

	Date: 29/11/2023

	Data generated by this script
	
		- drug analyses data uk.txt drug-specific analyses data in UK
		
*******************************************************************************/

* Start logging

	log using "$Logdir\3_figures\4a_creating drug fig data uk", name(creating_drug_fig_data_uk) replace
	
********************************************************************************	

* Install necessary packages

	net install grc1leg, from (http://www.stata.com/users/vwiggins)
	net install schemepack, from("https://raw.githubusercontent.com/asjadnaqvi/stata-schemepack/main/installation/") replace
	
	* Set the color scheme from schemepack
	set scheme tab3, perm
	
********************************************************************************	

* Create the dataset for the figure - retaining the estimates for the drug-specific analyses

	tempname myhandle	
	file open `myhandle' using "$Graphdir\data\drug analyses data uk.txt", write replace
	
	file write `myhandle' "outcome" _tab "drug" _tab "country" _tab "total" _tab "total_exp" _tab "or" _tab "lci" _tab "uci" _tab "risk" _tab "risk_lci" _tab "risk_uci" _n
	
	foreach outcome in preterm sga_pct {
		
		use "$Datadir\primary_analysis_dataset.dta", clear
		
		local y="`outcome'"
		if "`y'"=="preterm" {
		    
			replace drug_preg=0 if flag_37wk_initiators==1
			tab drug_preg
			
		}
		
		else {
		    
			tab drug_preg
			
		}
	    
		logit `outcome' i.drug_preg birth_yr i.mother_age_cat i.mother_ethn i.smoke_preg i.grav_hist_sb i.imd_practice i.parity i.asm_12mo i.ap_12mo CPRD_consultation_events_cat depression anxiety, or vce(cluster patid)
		
		forvalues n=0/11 {
			
			* Counts for the table	
			count if `outcome'==1 & drug_preg==`n'
			local exp_n=`r(N)' 
				
			count if drug_preg==`n' & `outcome'!=.
			local exp=`r(N)'
		    
			margins `n'.drug_preg
			matrix risk_table = r(table)
			local risk=risk_table[1,1]
			local lci=risk_table[5,1]
			local uci=risk_table[6,1]
			local risk_percent=(`risk')*100
			local risk_lci=(`lci')*100
			local risk_uci=(`uci')*100
		    
			local tot=`e(N)'
			lincom `n'.drug_preg, or 
			local or=`r(estimate)'
			local uci=`r(ub)'
			local lci=`r(lb)'
			
			file write `myhandle' ("`outcome'") _tab (`n') _tab ("uk") _tab (`tot') _tab %6.0fc (`exp_n') ("/") %7.0fc (`exp') _tab (`or') _tab (`lci') _tab (`uci') _tab (`risk_percent')  _tab (`risk_lci')  _tab (`risk_uci')  _n
			
		}
	}
	
	tempname myhandle	
	file open `myhandle' using "$Graphdir\data\any drug risk uk.txt", write replace
	
	file write `myhandle' "outcome" _tab "drug" _tab "country" _tab "total" _tab "total_exp" _tab "or" _tab "lci" _tab "uci" _tab "risk" _tab "risk_lci" _tab "risk_uci" _n
	
	foreach outcome in stillborn preterm postterm sga_pct lga_pct {
		
		use "$Datadir\primary_analysis_dataset.dta", clear
		
		local y="`outcome'"
		if "`y'"=="preterm" {
		    
			replace any_preg=0 if flag_37wk_initiators==1
			tab any_preg
			
		}
		
		else {
		    
			tab any_preg
			
		}
	    
		logit `outcome' i.any_preg birth_yr i.mother_age_cat i.mother_ethn i.smoke_preg i.grav_hist_sb i.imd_practice i.parity i.asm_12mo i.ap_12mo CPRD_consultation_events_cat depression anxiety, or vce(cluster patid)
		
		forvalues n=0/1 {
			
			* Counts for the table	
			count if `outcome'==1 & any_preg==`n'
			local exp_n=`r(N)' 
				
			count if any_preg==`n' & `outcome'!=.
			local exp=`r(N)'
		    
			margins `n'.any_preg
			matrix risk_table = r(table)
			local risk=risk_table[1,1]
			local lci=risk_table[5,1]
			local uci=risk_table[6,1]
			local risk_percent=(`risk')*100
			local risk_lci=(`lci')*100
			local risk_uci=(`uci')*100
		    
			local tot=`e(N)'
			lincom `n'.any_preg, or 
			local or=`r(estimate)'
			local uci=`r(ub)'
			local lci=`r(lb)'
			
			file write `myhandle' ("`outcome'") _tab (`n') _tab ("uk") _tab (`tot') _tab %6.0fc (`exp_n') ("/") %7.0fc (`exp') _tab (`or') _tab (`lci') _tab (`uci') _tab (`risk_percent')  _tab (`risk_lci')  _tab (`risk_uci')  _n
			
		}
	}
	
********************************************************************************

* Stop logging, translate .smcl into .pdf and erase .smcl

	log close creating_drug_fig_data_uk
	
	translate "$Logdir\3_figures\4a_creating drug fig data uk.smcl" "$Logdir\3_figures\4a_creating drug fig data uk.pdf", replace
	
	erase "$Logdir\3_figures\4a_creating drug fig data uk.smcl"
	
********************************************************************************