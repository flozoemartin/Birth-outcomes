/*******************************************************************************

	Generate data to include in the figure of prescribing over time for UK

	Author: Flo Martin

	Date: 29/11/2023

	Table generated by this script
	
		- add adjust data_uk.txt for meta-analysis
		
*******************************************************************************/

* Start logging

	log using "$Logdir\meta analysis\5a_creating add adjustment fig data uk", name(add_adjust_uk) replace
	
********************************************************************************

	tempname myhandle	
	file open `myhandle' using "$Graphdir\data\add adjust data_uk.txt", write replace
	
	file write `myhandle' "outcome" _tab "country" _tab "total" _tab "total_exp" _tab "total_unexp" _tab "or" _tab "lci" _tab "uci" _n
	
	foreach outcome in stillborn preterm postterm sga_pct lga_pct { 
	    
		* Maternal primary analysis
		
		use "$Datadir\primary_analysis_dataset.dta", clear
	
		gen cc_eth = 1 if birth_yr_cat!=. & mother_age_cat!=. & imd_practice!=. & parity!=. & ap_12mo!=. & asm_12mo!=. & depression!=. & anxiety!=. & CPRD_consultation_events_cat!=. & mother_bmi_cat!=. & smoke_preg!=. & grav_hist_sb!=. & mother_ethn!=.
	
		keep if cc_eth==1
		
		file write `myhandle' "`outcome'" _tab "uk"
		
		* Adjusted for all covariates
		
		* Recode incident users after 37 weeks' unexposed for the preterm delivery modesls
		local y="`outcome'"
		if "`y'"=="preterm" {
		    
			replace any_preg=0 if flag_37wk_initiators==1
			tab any_preg
			
		}
		
		else {
		    
			tab any_preg
			
		}
		
		* Counts for the table	
		count if `outcome'==1 & any_preg==1
		local exp_n=`r(N)' 
			
		count if any_preg==1 & `outcome'!=.
		local exp=`r(N)'
			
		count if `outcome'==1 & any_preg==0
		local unexp_n=`r(N)' 
			
		count if any_preg==0 & `outcome'!=.
		local unexp=`r(N)'
		
		logistic `outcome' i.any_preg birth_yr i.mother_age_cat i.imd_practice i.parity i.ap_12mo i.asm_12mo CPRD_consultation_events_cat depression anxiety i.mother_bmi_cat i.smoke_preg i.grav_hist_sb i.mother_ethn, vce(cluster patid) or
		
		local tot=`e(N)'
		lincom 1.any_preg, or 
		local or=`r(estimate)'
		local uci=`r(ub)'
		local lci=`r(lb)'
		
		file write `myhandle' _tab (`tot') _tab %6.0fc (`exp_n') ("/") %6.0fc (`exp') _tab %6.0fc (`unexp_n') ("/") %7.0fc (`unexp') _tab (`or') _tab (`lci') _tab (`uci') _n
		
	}
	
	import delimited using "$Graphdir\data\add adjust data_uk.txt", clear
	
	replace outcome="sga" if outcome=="sga_pct"
	replace outcome="lga" if outcome=="lga_pct"
	
	save "$Graphdir\data\uk_counts_addadjust.dta", replace

********************************************************************************

* Stop logging, translate .smcl into .pdf and erase .smcl

	log close add_adjust_uk
	
	translate "$Logdir\meta analysis\5a_creating add adjustment fig data uk.smcl" "$Logdir\meta analysis\5a_creating add adjustment fig data uk.pdf", replace
	
	erase "$Logdir\meta analysis\5a_creating add adjustment fig data uk.smcl"
	
********************************************************************************
