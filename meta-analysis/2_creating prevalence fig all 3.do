
/*******************************************************************************

	Generate data to include in the figure of prescribing over time for Norway

	Author: Flo Martin

	Date: 29/11/2023

	Figure generated by this script
	
		- outcome_prevalence.pdf prevalence of each of the outcomes in the 3 countries
		
*******************************************************************************/

* Start logging

	log using "$Logdir\meta analysis\2_creating prevalence fig all 3", name(creating_prevalence_fig_all_3) replace
	
********************************************************************************

* UK

	use "$Datadir\primary_analysis_dataset.dta", clear
	
	foreach x in stillborn preterm postterm sga_pct lga_pct {
		
		preserve
		
			drop outcome
		
			gen outcome="`x'"
			
			if outcome=="sga_pct" | outcome=="lga_pct" {
				
				drop if birth_weight==. | child_male==.
				
			}
			
			else {
				
				drop if `x'==.
				
			}
			
			bysort `x': egen seq=seq()
			bysort `x': egen n=max(seq)
			drop seq
				
			egen seq=seq()
			egen denom=max(seq)
			drop seq
				
			keep if `x'==1
				
			keep n denom outcome
			duplicates drop
			
			save "$Tempdatadir\count_`x'.dta", replace
		
		restore
		
	}
	
	use "$Tempdatadir\count_stillborn.dta", clear
	
	foreach x in preterm postterm sga_pct lga_pct {
		
		append using "$Tempdatadir\count_`x'.dta"
		
	}
	
	order outcome n denom
	
	gen pct = (n/denom)*100
	
	keep outcome pct
	rename pct pct_uk
	
	save "$Graphdir\data\prevalence_uk.dta", replace
	
* Norway

	import delimited using "$NOTabledir\prevalence of outcomes.txt", clear
	keep v1 v2
	rename v1 outcome
	
	local v v2
	local p2 strpos(`v', ")") - 1 
	local p1 strpos(`v', "(") + 1

	gen swanted2 = word(`v', -1)
	replace swanted2 = subinstr(swanted2, "(", "", .) 
	replace swanted2 = subinstr(swanted2, ")", "", .) 
	gen wanted2 = real(swanted2)
	
	rename wanted2 pct_no
	keep outcome pct_no
	
	keep if outcome=="Stillborn" | outcome=="Neonatal death" | outcome=="Preterm delivery (<37 weeks')" | outcome=="Post-term delivery (>42 weeks')" | outcome=="Small for gestational age (SGA, <10th percentile)" | outcome=="Large for gestational age (LGA, >90th percentile)" | outcome=="Apgar score < 7 at 5 mins"
	
	egen seq=seq()
	
	replace outcome="stillborn" if seq==1
	replace outcome="neonatal_death" if seq==2
	replace outcome="apgar5_bin" if seq==3
	replace outcome="preterm" if seq==4
	replace outcome="postterm" if seq==5
	replace outcome="sga_pct" if seq==6
	replace outcome="lga_pct" if seq==7
	
	drop seq
	
	save "$Graphdir\data\prevalence_no.dta", replace
	
* Merge the countries
	
	use "$Graphdir\data\prevalence_uk.dta", clear
	merge 1:1 outcome using "$Graphdir\data\prevalence_no.dta", nogen
	merge 1:1 outcome using "$Graphdir\data\prevalence_se.dta", nogen
	
	gen seq=1 if outcome=="stillborn"
	replace seq=2 if outcome=="neonatal_death"
	replace seq=3 if outcome=="preterm"
	replace seq=4 if outcome=="postterm"
	replace seq=5 if outcome=="sga_pct"
	replace seq=6 if outcome=="lga_pct"
	replace seq=7 if outcome=="apgar5_bin"
	
	sort seq
	
	rename pct_uk pct1
	rename pct_no pct2
	rename pct_se pct3
	
	reshape long pct, i(outcome seq) j(country)
	
	* Generate variables for second axis (to make box around the graph using scatter)
	gen x=0
	gen y=10
	
	replace seq = seq-0.3 if country==1
	replace seq = seq+0.3 if country==3

	gen labels = pct
	format labels %3.1fc
	
	tostring(labels), gen(labels_str) format(%3.1fc) force
	
	gen labels_bold = "{bf:" + labels_str + "}"
	
	tw ///
	(bar pct seq if country==2, barwidth(0.25) base(0) bfc("127 165 177") lcolor(black) lw(thin)) ///
	(bar pct seq if country==1, barwidth(0.25) base(0) bfc("127 165 177") lcolor(black) lw(thin)) ///
	(bar pct seq if country==3, barwidth(0.25) base(0) bfc("127 165 177") lcolor(black) lw(thin)) ///
	(scatter labels seq if country==2, mlabel(labels_bold) ms(O) mlabposition(12) mfcolor("51 76 88") mlc(black) mlw(thin)) ///
	(scatter labels seq if country==1, mlabel(labels_bold) ms(T) mlabposition(12) mfcolor("51 76 88") mlc(black) mlw(thin)) ///
	(scatter labels seq if country==3, mlabel(labels_bold) ms(D) mlabposition(12) mfcolor("51 76 88") mlc(black) mlw(thin)) ///
	, xline(1, lwidth(19) lc(gs12*0.5)) xline(3, lwidth(19) lc(gs12*0.5)) xline(5, lwidth(19) lc(gs12*0.5)) xline(7, lwidth(19) lc(gs12*0.5)) /// 
	yline(2, lwidth(0.25) lc(gs12*0.75)) yline(4, lwidth(0.25) lc(gs12*0.75)) yline(6, lwidth(0.25) lc(gs12*0.75)) yline(8, lwidth(0.25) lc(gs12*0.75)) yline(10, lwidth(0.25) lc(gs12*0.75)) ///
	legend(row(1) order(4 "Norway" 5 "UK" 6 "Sweden") pos(6) size(vsmall)) ///
	yscale(range(0 11.5)) ylabel(0(2)10, nogrid) ytitle("{bf}Proportion of pregnancies (%)", size(vsmall)) ///
	xscale(range(0.7 7.3))  xlabel(1 "{bf}Stillbirth" 2 "{bf}Neonatal death" 3 `" "{bf:Preterm}" "{bf:delivery}" "' 4 `" "{bf:Post-term}" "{bf:delivery}" "' 5 "{bf}SGA" 6 "{bf}LGA" 7 `" "{bf:Low Apgar}" "{bf:score at 5 minutes}" "', noticks nogrid labsize(vsmall)) xtitle("") || ///
	scatter y x, msymbol(i) yaxis(2) xaxis(2) ylab("", axis(2) notick nolab) xlab(, axis(2) notick nolab) ytitle("", axis(2)) xtitle("", axis(2)) ///
	title("{bf}Prevalence of adverse birth outcomes in UK, Norway, & Sweden", size(small) color(black)) ///
	plotregion(margin(1.9 1.9 0 0)) name(outcome_prevalence, replace)
	
* Set working directory for saving graphs and save as PDF
	
	cd "$Graphdir\"
	graph export outcome_prevalence.pdf, replace

********************************************************************************

* Erase unnecessary datasets

	foreach x in stillborn preterm postterm sga_pct lga_pct {
		
		erase "$Tempdatadir\count_`x'.dta"
		
	}

* Stop logging, translate .smcl into .pdf and erase .smcl

	log close creating_prevalence_fig_all_3
	
	translate "$Logdir\meta analysis\2_creating prevalence fig all 3.smcl" "$Logdir\meta analysis\2_creating prevalence fig all 3.pdf", replace
	
	erase "$Logdir\meta analysis\2_creating prevalence fig all 3.smcl"
	
********************************************************************************
